# Maintainer: ArchAssault <team@archassault.org>
pkgname=blacksheepwall
pkgver=2.0.3
pkgrel=1
pkgdesc='hostname reconnaissance tool written in Go'
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url='https://github.com/tomsteele/blacksheepwall'
license=('CUSTOM')
makedepends=('go' 'git' 'mercurial')
source=("https://raw.githubusercontent.com/tomsteele/blacksheepwall/master/blacksheepwall.go" "https://raw.githubusercontent.com/tomsteele/blacksheepwall/master/LICENSE")
sha512sums=('fb38456cd4701d7099c2f3bb132233289728b7180877a045c4825733c4b5780e1ea961317877d84f2880cd3a79643f32194ac54f6ba680b6e23398eec0df85cc'
            '2535330ca644b9c5d94bae692847d7bfcabfc2509f9f78fb534a952c64b03afbbcc2c56503fc6ada232934c044afb716468de4aed7bd3b7c10226f46769caa74')
_gourl=github.com/tomsteele/blacksheepwall/bsw

prepare(){
  GOPATH="$srcdir" go get -fix -v -x ${_gourl}
}

build() {
  #
  # Arch Linux doesn't support SSE2 for i686 (to support older CPUs)
  # The SSE2 version is _a lot_ faster for i686.
  # This line disables it.
  #
  export GO386=387

  # Enable ARM crosscompilation for linux
  export GOOS=linux
  if [ "$CARCH" == 'x86_64' ]; then
    export GOARCH=amd64
  elif [ "$CARCH" == 'i686' ]; then
    export GOARCH=386
  elif [ "$CARCH" == 'armv6h' ] || [ "$CARCH" == 'armv7h' ];then
    export GOARCH=arm
  fi
  
  GOPATH="$srcdir" go build -v -o "$pkgname"
}


package() {
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname"
}


