# Maintainer: ArchAssault <team@archassault.org>
pkgname=dff-git
pkgver=r109.6a011ac
pkgrel=1
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archassault')
pkgdesc="Digital Forensic Framework"
url='http://www.digital-forensic.org'
license=('GPL2')
depends=('swig' 'python2' 'python2-magic' 'ffmpeg' 'python2-pyqt4' 'libbfio' 'libewf' 'libpff' 'reglookup' 'afflib' 'python2-apsw' 'python2-pillow' 'icu')
makedepends=('cmake' 'doxygen')
source=("dff::git+http://git.digital-forensic.org/dff.git" "libav9-dff.patch")
sha512sums=('SKIP'
            '2fd686261d8450cb3bfa867936ae1c348c09143c1e84b90ffb871ad26f218e912936d082902e34ae69ba9684557228092cc2523ba7d8c524c4c0dcde7da95967')
provides=('dff')
conflicts=('dff')
replaces=('dff')

prepare() {
  cd "$srcdir/dff"
  sed -i 's|2.0.7|2.0.12|' CMakeLists.txt
  sed -i 's|git:\(//.*\)$|http:\1.git|' .gitmodules
  git submodule init
  git submodule update
  cd $srcdir/dff/dff/api/gui/video
  patch -i $srcdir/libav9-dff.patch  
}

pkgver() {
  cd "$srcdir/dff"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
  cd "$srcdir/dff"
  mkdir -p build && cd build
  cmake -DCMAKE_VERBOSE_MAKEFILE=ON -DINSTALL=YES -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE=/usr/bin/python2 -DQT_LANGUAGE_COMPILER:FILEPATH=/usr/bin/lrelease-qt4 ..
  make
}

package() {
  cd "$srcdir/dff/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 $srcdir/dff/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 $srcdir/dff/LICENSE-THIRDPARTY "$pkgdir/usr/share/licenses/$pkgname/LICENSE-THIRDPARTY"
  for i in $pkgdir/usr/bin/dff-gui $pkgdir/usr/lib/python2.7/site-packages/dff/modules/statistics/fileschart/chart.py $pkgdir/usr/bin/dff $pkgdir/usr/lib/python2.7/site-packages/dff/modules/ram/volatility/thirdparty/progressbar.py $pkgdir/usr/lib/python2.7/site-packages/dff/modules/viewer/hexedit/nceditor.py $pkgdir/usr/lib/python2.7/site-packages/dff/modules/metadata/metaexif/EXIF.py; do sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|' $i;done
}

