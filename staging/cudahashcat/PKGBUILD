# Maintainer: ArchAssault <team archassault org>
pkgname=cudahashcat
_pkgname=cudaHashcat
pkgver=1.32
pkgrel=1
pkgdesc="Worlds fastest WPA cracker with dictionary mutation engine (NV ForceWare)"
url=('http://hashcat.net/oclhashcat/')
arch=('i686' 'x86_64')
license=('custom')
depends=('bash' 'libcl' 'nvidia>=346' 'nvidia-utils>=346')
source=("http://hashcat.net/files/$_pkgname-$pkgver.7z")
sha512sums=('fe370337e5cd2bbf0c53d3df440c8efdfcb48524f2f2d99f5caa598c40768b54ce44771e836ccb361d991365c526982e0938b3b295881ae6b1fc063d3985d29a')
options=('!strip')

prepare() {
  cd $_pkgname-$pkgver
  sed -i '1,2d;s|${OCLHASHCAT_ROOT}/oclHashcat64.bin "${OCLHASHCAT_ROOT}"/oclHashcat32.bin "${OCLHASHCAT_ROOT}"/cudaHashcat64.bin "${OCLHASHCAT_ROOT}"/cudaHashcat32.bin|'"$_pkgname"'|' extra/tab_completion/oclHashcat64.sh
}

package() {
  install -dm755 "$pkgdir/opt/$pkgname"
  cp -a --no-preserve=ownership  $_pkgname-$pkgver/* "$pkgdir/opt/$pkgname/"
  cd "$pkgdir/opt/$pkgname"

  # Install bash completion
  install -Dm644 extra/tab_completion/oclHashcat64.sh "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  rm -rf extra/tab_completion

  # Install the executable script
  install -dm755 "$pkgdir"/usr/{bin,share/licenses/$pkgname}/
  if [[ "$CARCH" = "x86_64" ]]; then
    rm ${_pkgname}32.bin
    echo -e "#!/bin/bash\n/opt/$pkgname/${_pkgname}64.bin \$@" > "$pkgdir/usr/bin/$_pkgname"
    ./${_pkgname}64.bin --eula > "$pkgdir/usr/share/licenses/$pkgname/EULA.txt"
  else
    rm ${_pkgname}64.bin
    echo -e "#!/bin/bash\n/opt/$pkgname/${_pkgname}32.bin \$@" > "$pkgdir/usr/bin/$_pkgname"
    ./${_pkgname}32.bin --eula > "$pkgdir/usr/share/licenses/$pkgname/EULA.txt"
  fi
  chmod 755 "$pkgdir/usr/bin/$_pkgname"

  # Cleanup win-executables
  find . -regextype posix-extended -regex '.*\.(cmd|exe)$' -exec rm '{}' \;

  # Fix permissions
  cd "$pkgdir"
  find . -regextype posix-extended -regex '.*\.(txt|llvmir|hash|rule|hcmask|hcstat|dict)$' -exec chmod 644 '{}' \;
  find . -regextype posix-extended -regex '.*\.(sh|bin)$' -exec chmod 755 '{}' \;
}

