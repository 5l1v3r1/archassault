# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Prurigro

pkgname=android-ndk
pkgver=r9d
pkgrel=1
pkgdesc="Android C/C++ developer kit"
arch=('i686' 'x86_64')
groups=('archassault' 'archassault-mobile')
url="http://developer.android.com/sdk/ndk/index.html"
license=('Apache' 'GPL3' 'LGPL' 'BSD' 'MIT' 'PSF' 'custom')
depends=('android-sdk')
options=('!emptydirs' '!strip' 'staticlibs')
install="${pkgname}.install"

source=("https://dl-ssl.google.com/android/ndk/${pkgname}-${pkgver}-linux-x86.tar.bz2"
        "${pkgname}.sh"
        "SGI.gles.license.txt")
sha512sums=('9ba1603d3ac5daf8816dbc7ba998d6b93d4f3d197a30beaaccb02fefe785554769b453462c90fe19194200808b38128396e77c32425c8d0872652c81a916304e'
            '6f139a4fbc08e268894241bb9377b1e485a362fc16970aba4f72eda1c20413005ffc74a4e9cfd2613788a5d38602b79e2882e20e8ef53d70770010e813603d55'
            '6825a3d22170186938bcc629a5208687e81625f2897d7119b14bc3563735f3948998aad0d14e1b0815cf0b20d1b59681d795fb3623232655e18b2ecaa39ce3fb')
[[ "$CARCH" = "x86_64" ]] && source[0]="http://dl.google.com/android/ndk/${pkgname}-${pkgver}-linux-${CARCH}.tar.bz2" && sha512sums[0]='b6b663533785fb6cbc10e467a109cf47f4a49d87abc72f03439ac7b3aa57350e1602d766793bf6c3c5f6e4f4ab8733311ef1c761ac335691566b2e936f853af3' && ARCH="$CARCH" || ARCH="x86"

prepare() {
    cd ${pkgname}-${pkgver}/prebuilt

    # SET PYTHON PATHS TO PACKAGE BINARIES
    for file in $(grep -lr -e '^#!.*env python'); do sed -i -e 's|^#! */usr/bin/env python|#!/opt/android-ndk/prebuilt/linux-'$ARCH'/bin/python|' "$file"; done
    for file in $(grep -li -e '^#!/tmp/ndk-andrewhsieh/buildhost/install' linux-${ARCH}/bin/*); do sed -i -e 's|^#! */tmp/ndk-andrewhsieh/buildhost/install|#!/opt/android-ndk|' "$file"; done
    sed -i 's|\ /usr/local/bin/python|/opt/android-ndk/prebuilt/linux-'$ARCH'/bin/python2.7|' linux-${ARCH}/lib/python2.7/cgi.py
    sed -i 's|/usr/bin/python|/opt/android-ndk/prebuilt/linux-'$ARCH'/bin/python2.7|' common/scan-build/set-xcode-analyzer
}

package() {
    # INSTALL PROFILE SCRIPT
    install -Dm755 ${pkgname}.sh "${pkgdir}/etc/profile.d/android-ndk.sh"

    # INSTALL NDK
    install -d "${pkgdir}/opt"
    cp -a ${pkgname}-${pkgver} "${pkgdir}/opt/${pkgname}/"
    chmod 644 "${pkgdir}/opt/android-ndk/prebuilt/linux-${ARCH}/lib/libpython2.7.a"

    # INSTALL LICENSES
    install -Dm644 SGI.gles.license.txt "${pkgdir}/usr/share/licenses/${pkgname}/SGI.gles"
    cd ${pkgname}-${pkgver}
    install -Dm644 sources/android/crazy_linker/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/BSD.chromium"
    install -Dm644 sources/third_party/googletest/googletest/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/BSD.google"
    install -Dm644 sources/cxx-stl/stlport/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/CUSTOM.stlport"
    install -Dm644 sources/cxx-stl/llvm-libc++/libcxx/LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/BSD-MIT.llvm-libc++"
    install -Dm644 sources/cxx-stl/llvm-libc++abi/libcxxabi/LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/BSD-MIT.libc++abi"
    install -Dm644 prebuilt/linux-${ARCH}/lib/python2.7/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/PSF.python2"
    install -Dm644 prebuilt/common/scan-build/LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/BSD.llvm"
    install -Dm644 samples/san-angeles/jni/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LGPL-BSD.san_angeles_observation_opengl-es"
    install -Dm644 samples/san-angeles/jni/license-BSD.txt "${pkgdir}/usr/share/licenses/${pkgname}/BSD.san_angeles_observation_opengl-es"
    install -Dm644 docs/text/LICENSES.text "${pkgdir}/usr/share/licenses/${pkgname}/LICENSES.txt"
}
