# Maintainer: ArchAssault <team@archassault.org>
pkgname=shiboken
pkgver=1.2.2
pkgrel=1
pkgdesc="Python bindings generator that uses API Extractor and outputs CPython code."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
options=('!distcc')
license=('LGPL')
url="http://www.pyside.org"
depends=("python2" "python3" "qt4>=4.8" 'libxslt')
optdepends=("python-sphinx: Documentation")
makedepends=('cmake')
conflicts=("libshiboken")
provides=("libshiboken" "shiboken")
source=("http://download.qt-project.org/official_releases/pyside/shiboken-$pkgver.tar.bz2")
sha512sums=('9f5c1219ce699df6b5ee4bd68cf422cb69476bd062346fcc8b4d34d314001ba60dbd52a73d772aec16285a0aabb600034630beba754ca54f5e6a4b744a720073')

build(){
  cd "$srcdir/shiboken-$pkgver"
  mkdir -p build_python2 && cd build_python2
  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
            -DCMAKE_BUILD_TYPE=Release   \
            -DBUILD_TESTS=OFF            \
            -DPYTHON_EXECUTABLE=/usr/bin/python2 \
            -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
            -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
            -DQT_QMAKE_EXECUTABLE=qmake4
  make

  cd "$srcdir/shiboken-$pkgver"
  mkdir -p build && cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
  -DCMAKE_BUILD_TYPE=Release   \
  -DBUILD_TESTS=OFF            \
  -DUSE_PYTHON3=yes            \
  -DQT_QMAKE_EXECUTABLE=qmake4
  make
}

package(){
  cd "$srcdir/shiboken-$pkgver/build"
  make DESTDIR="$pkgdir" install

  cd data
  install -D -m 755 ShibokenConfig.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver"
  install -D -m 755 ShibokenConfigVersion.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
  install -D -m 755 ShibokenConfig.cpython-34m.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
  install -D -m 755 shiboken.pc "$pkgdir/usr/lib/pkgconfig/"

  cd "$srcdir/shiboken-$pkgver/build_python2"
  make DESTDIR="$pkgdir" install

  cd data
  install -D -m 755 ShibokenConfig-python2.7.cmake "$pkgdir/usr/lib/cmake/Shiboken-$pkgver/"
}
