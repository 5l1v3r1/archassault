# Maintainer: ArchAssault <team@archassault.org>
_sdkname=android-sdk
_pkgname=platform-tools
pkgname=${_sdkname}-${_pkgname}
pkgver=r20
pkgrel=1
pkgdesc="Platform-Tools for the Google Android SDK (adb and fastboot)"
groups=('archassault' 'archassault-mobile')
arch=('i686' 'x86_64')
url='http://developer.android.com/sdk/index.html'
license=('custom')
options=('!strip')

depends=('gcc-libs'
         'zlib'
         'python2')
[[ $CARCH = x86_64 ]] && \
depends=('lib32-gcc-libs'
         'lib32-zlib'
         'python2')

source=("http://dl-ssl.google.com/android/repository/${_pkgname}_${pkgver}-linux.zip"
        "adb.service"
        "license.html")
sha512sums=('a3ce19a0103251a15115a449c1561ef2f056efcbc3f8def338acad73a89067e33dc6a5c0bd003eee02e8c8e29c77b01832db742e78c002ff5e1b35d58c2dfc82'
            'faf315d2bb7a9caa099db32e21451a1fd8fcc76475f25b36742356adc4f758e6fc7512106f9539b2db687c9566ef5889a4fa051c3c1a84555090d4b175e485d4'
            '23f1533682c39e2ae2ae40083de343abcd6ed0a523eacdac58768067f8f450b1cec30073993dd7b972a5b8125136ed29a043ae894dff046fd75bb04c4d21a0f4')

prepare() {
    sed -i 's|/usr/bin/env python|/usr/bin/env python2|' ${_pkgname}/systrace/*.py
}

package() {
    install -Dm644 adb.service "${pkgdir}/usr/lib/systemd/system/adb.service"
    install -Dm644 license.html "${pkgdir}/usr/share/licenses/${_sdkname}-${_pkgname}/license.html"

    install -d "${pkgdir}/etc/profile.d"
    echo "export PATH="'$PATH'":/opt/${_sdkname}/${_pkgname}" > "${pkgdir}/etc/profile.d/${pkgname}.sh"
    echo "setenv PATH "'${PATH}'":/opt/${_sdkname}/${_pkgname}" > "${pkgdir}/etc/profile.d/${pkgname}.csh"
    chmod 755 "${pkgdir}/etc/profile.d/${pkgname}."{csh,sh}

    install -dm775 "${pkgdir}/opt/${_sdkname}"
    cp -a platform-tools "${pkgdir}/opt/${_sdkname}/"
    chmod -R ugo+rX "${pkgdir}/opt/${_sdkname}/${_pkgname}"
    chmod g+rwX "${pkgdir}/opt/${_sdkname}/${_pkgname}"
}
