srcname='cryptokit'
pkgname="ocaml-${srcname}"
pkgver='1.6'
pkgrel=3
pkgdesc='Cryptographic primitives for OCaml'
arch=('i686' 'x86_64')
url="http://pauillac.inria.fr/~xleroy/software.html#cryptokit"
license=('LGPL')
depends=('ocaml' 'zlib') # OCaml as depend, or only makedepends?
makedepends=('ocaml' 'ocaml-findlib')
source=('http://forge.ocamlcore.org/frs/download.php/891/cryptokit-1.6.tar.gz')
md5sums=('341a4ca2db7fc7755b08d2e54a6874e1')
options=('staticlibs')

build() {
    cd "$srcdir/${srcname}-${pkgver}"
    ./configure

    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make
}


package() {
    mkdir -p "$pkgdir/$(ocamlfind printconf destdir)"
    mkdir -p "$pkgdir/$(ocamlfind printconf destdir)/stublibs"
    cd "$srcdir/${srcname}-${pkgver}"
    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make install
}
