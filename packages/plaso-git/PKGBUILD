# Maintainer: ArchAssault <team archassault org>
pkgname=plaso-git
pkgver=20141117.r776
pkgrel=1
groups=('archassault' 'archassault-forensics')
pkgdesc="A Python-based backend engine for the tool log2timeline."
arch=('any')
url='https://github.com/log2timeline/plaso'
license=('APACHE')
depends=('python2')
makedepends=('git' 'python2-setuptools')
provides=('plaso' 'log2timeline')
source=("${pkgname}::git+https://github.com/log2timeline/plaso.git")
sha512sums=('SKIP')

prepare(){
  grep -iRl 'python' "$pkgname" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

pkgver() {
    cd "${pkgname}"
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "${pkgname}"
  python2 setup.py install --root=$pkgdir --optimize=1
}
