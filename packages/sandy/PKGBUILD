pkgname=sandy
pkgver=20131227
pkgrel=1
groups=('archassault' 'archassault-scanner')
pkgdesc="An open-source Samsung phone encryption assessment framework."
url="https://github.com/donctl/sandy"
arch=('any')
license=('GPL2')
depends=('python2' 'python2-pexpect' 'python2-pbkdf2' 'python2-crypto')
source=("git+https://github.com/donctl/sandy.git")
options=('!strip')
md5sums=('SKIP')

pkgver() {
  date +%Y%m%d
}

prepare(){
  grep -iRl 'python' "$srcdir/sandy" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/sandy"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"

cat > "$pkgdir/usr/bin/sandy" << EOF
#!/bin/sh
cd /usr/share/sandy
python2 main.py "\$@"
EOF
chmod +x "$pkgdir/usr/bin/sandy"
}
