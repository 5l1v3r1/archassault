# Maintainer: ArchAssault <team archassault org>
pkgname=capstone
pkgver=3.0.1
pkgrel=1
groups=('archassault' 'archassault-reversing')
pkgdesc="A lightweight multi-platform, multi-architecture disassembly framework."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.capstone-engine.org/index.html'
license=('GPL')
depends=('glibc')
makedepends=('python2-setuptools' 'python-setuptools')
provides=('python2-capstone' 'python-capstone')
source=("http://www.capstone-engine.org/download/$pkgver/capstone-$pkgver.tgz" "python.patch")
sha512sums=('d4fdcc658273fd9aa844fdaea9c01e7da7be1cc09a7fc62c631ac35783b0ed13e32b5ac89c49b4cbeab5c55bed0da4f5e62c3190bceff5abd0819209913979ef'
            'd7d85469ab382e02b13f5bf3345bb4241070997bd5d88611511cb1d839865172f757db09e05ccb02d40699b5429d818d8700d4b09b3d1fe8b3028d647cec5e48')

prepare(){
  cd $srcdir/$pkgname-$pkgver/bindings/python
  sed -i 's|^python|python2|' Makefile
  patch -i $srcdir/python.patch
  cd $srcdir/$pkgname-$pkgver/bindings/java
  sed -i 's|python|python2|' Makefile
  cd $srcdir/$pkgname-$pkgver/bindings/ocaml
  sed -i 's|python|python2|' Makefile
}


build() {
  cd "$srcdir/capstone-$pkgver"
  ./make.sh
  cd $srcdir/$pkgname-$pkgver/bindings/python
  make
  #cd $srcdir/$pkgname-$pkgver/bindings/java
  #make
  #cd $srcdir/$pkgname-$pkgver/bindings/ocaml
  #make  
}

package() {
  cd "$srcdir/capstone-$pkgver"
  make DESTDIR="$pkgdir" install
  cd bindings/python
  # install py2 bindings
  sed -i 's|--root=|--root='"$pkgdir"'|' Makefile
  msg "Building python2 bindings"
  make DESTDIR="$pkgdir" install
  # install py3 bindings
  msg "Building python bindings"
  make DESTDIR="$pkgdir" install3
}
