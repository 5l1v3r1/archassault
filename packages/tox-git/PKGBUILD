# Maintainer: ArchAssault <team at archassault org>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: naxuroqa <naxuroqa at gmail.com>

_pkgname=tox
pkgname=${_pkgname}-git
_pkgver=fc77cfe
pkgver=r2705.fc77cfe
pkgrel=1
epoch=1
pkgdesc="Secure, configuration-free, P2P Skype replacement backend"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://${_pkgname}.im/"
license=('GPL3')
depends=('libsodium' 'opus' 'libvpx')
makedepends=('git' 'autoconf' 'automake' 'check' 'libtool')
optdepends=('qtox-git: qt-based frontend'
            'utox-git: lightweight tox client'
            'venom-git: gtk/vala-based frontend'
            'toxic-git: ncurses-based frontend')
conflicts=("$_pkgname")
provides=("$_pkgname")
replaces=("$_pkgname")

source=("${pkgname}::git://github.com/irungentoo/${_pkgname}core.git#commit=${_pkgver}")
sha512sums=('SKIP')

pkgver() {
    cd $pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd $pkgname
    libtoolize --force
    ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
}
