# Maintainer: ArchAssault <team@archassault.org>
_pkgname=wifite
pkgname=${_pkgname}-git
pkgver=20130817.r122.a6535c5
pkgrel=1
groups=('archassault' 'archassault-wireless' 'archassault-cracker')
pkgdesc="A tool to attack multiple WEP and WPA encrypted networks at the same time"
arch=('any')
url="https://github.com/derv82/${_pkgname}"
license=('GPL')
depends=('python2' 'aircrack-ng' 'net-tools' 'wireless_tools' 'which' 'iw')
optdepends=('reaver: scan and attack WPS-enabled routers'
            'wireshark-cli: packet sniffing support'
            'cowpatty: handshake detection support')
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")

source=("git+https://github.com/derv82/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    sed -i 's|/usr/bin/python|/usr/bin/python2|g' ${_pkgname}/${_pkgname}.py
}

package() {
    install -Dm755 ${_pkgname}/${_pkgname}.py "${pkgdir}/usr/bin/${_pkgname}"
}
