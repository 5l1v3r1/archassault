# Maintainer: ArchAssault <team@archassault.org>

_pkgname=i2pd
pkgname=${_pkgname}-git
pkgver=20150105.r1361
pkgrel=1
pkgdesc="I2P router written in C++"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="https://github.com/PrivacySolutions/i2pd"
license=('GPL2')
groups=('archassault' 'archassault-encryption')
depends=('crypto++' 'boost-libs' 'systemd')
makedepends=('git' 'boost')
backup=("etc/default/${_pkgname}.conf")
install="${pkgname}.install"

source=("git+${url}.git"
        "${_pkgname}.service"
        "${_pkgname}.tmpfiles.conf"
        "${_pkgname}.conf")
sha512sums=('SKIP'
            'ecb028675a422c159d1d91ba430e74dbc785bf445865e7af2bf1e3178d10c2db0a7e6bcc32b8db4224f8788aff521907862a5c9cbed78251b3083ee3c11f7d7e'
            'e86ba75e5181b1e20637bbb19c827664a1746cbb387fa5d57e10d9648143a5b684eb88ec877c721033ccf5a6578ed9c5dbb9ef2a9ea79b5fbe4ddf32dfd6640d'
            '2e816538c766ae15ee405ac751c64e7512765e81f7e7b56dab1c96fe8efd2969c688e5545d2672c97d4f09612089762ca45dd27cb9676aca512cd7b22448e4af')

pkgver() {
    cd $_pkgname
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
    cd $_pkgname
    make
}

package(){
    install -Dm755 ${_pkgname}/i2p "$pkgdir"/usr/bin/i2p
    install -Dm644 ${_pkgname}.service "$pkgdir"/usr/lib/systemd/system/${_pkgname}.service
    install -Dm644 ${_pkgname}.tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/${_pkgname}.conf
    install -Dm644 ${_pkgname}.conf "$pkgdir"/etc/default/${_pkgname}.conf
}

