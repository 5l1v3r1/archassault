# Maintainer: ArchAssault <team@archassault.org>

_pkgname=i2pd
pkgname=${_pkgname}-git
pkgver=20150118.r1441
pkgrel=1
pkgdesc="I2P router written in C++"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="https://github.com/PrivacySolutions/i2pd"
license=('GPL2')
groups=('archassault' 'archassault-encryption')
depends=('crypto++' 'boost-libs' 'systemd')
makedepends=('git' 'boost')
backup=("etc/default/${_pkgname}.conf")
install=${pkgname}.install

source=("git+${url}.git"
        "${_pkgname}.service"
        "${_pkgname}.tmpfiles.conf"
        "${_pkgname}.conf")
sha512sums=('SKIP'
            '268f9405491c521b1385dd103d242c96f4a78c5c33414d350b0e4cb6099bb5ef6b86485799fb3ad5701a6e7ad52f08d1fa08257d757b709afe0433cb07827817'
            '3a8948fedf2862209b02914bfa26f06f1fe56f0b83591ebefd2de63c2dc4a210af6933f2ad8cd02c21ae8d2f377f1bd9eba821a45459b130775f523faa808586'
            '2e816538c766ae15ee405ac751c64e7512765e81f7e7b56dab1c96fe8efd2969c688e5545d2672c97d4f09612089762ca45dd27cb9676aca512cd7b22448e4af')

pkgver() {
    cd $_pkgname
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
    cd $_pkgname
    make -j1
}

package(){
    install -Dm755 ${_pkgname}/i2p "$pkgdir"/usr/bin/i2p
    install -Dm644 ${_pkgname}.service "$pkgdir"/usr/lib/systemd/system/${_pkgname}.service
    install -Dm644 ${_pkgname}.tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/${_pkgname}.conf
    install -Dm644 ${_pkgname}.conf "$pkgdir"/etc/default/${_pkgname}.conf
}

