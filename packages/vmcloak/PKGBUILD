# Maintainer: ArchAssault <team@archassault.org>
pkgname=vmcloak
pkgver=0.2
pkgrel=1
pkgdesc="Automated Virtual Machine Generation and Cloaking for Cuckoo Sandbox."
url="https://github.com/jbremer/vmcloak"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('archassault' 'archassault-malware')
depends=('cdrtools' 'virtualbox' 'dmidecode' 'lshw' 'python2-lockfile' 'python2-sphinx')
makedepends=('python2-setuptools' 'unzip')
conflicts=('vmcloak-git')
replaces=('vmcloak-git')
source=("https://github.com/jbremer/vmcloak/archive/${pkgver}.zip")
sha512sums=('f2ee5d7a17d3f8fcfb39ddff9d647a9a9307bcd1b229efe68fc8b539ee1b7e586e70a29fd9f5f21741e80e81b129e1143931e9a11c1c0569fec088baa46162bd')

prepare() {
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

package() {
  cd vmcloak-"${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 docs/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt" 
}


