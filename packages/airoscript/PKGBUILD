pkgname=airoscript
pkgver=20140105
pkgrel=1
pkgdesc="Script to simplify the use of aircrack-ng tools"
arch=('any')
url="http://midnightresearch.com/projects/wicrawl/"
groups=('archassault' 'archassault-wireless')
license=('GPL')
depends=('mdk3' 'aircrack-ng' 'dhclient')
options=(!makeflags)
makedepends=('git')
source=('git+http://github.com/patrickhno/Airoscript'
        'Makefile.patch'
        'airoscript.desktop')
md5sums=('SKIP'
         '441dd3a5f804f3c65c4da93b367199c1'
         '60954cc909417e196a3baeefd6a4c5db')

pkgver() {
  cd Airoscript
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Airoscript"
  patch -Np1 < "$srcdir/Makefile.patch"
  find -type f -exec sed -i 's|/sbin|/bin|g' {} \;
}

package() {
  cd "$srcdir/Airoscript"
  install -Dm644 "$srcdir/airoscript.desktop" "$pkgdir/usr/share/applications/airoscript.desktop"
  rm -rf src/locale/*
  make "prefix=$pkgdir/usr" "aircrack_prefix=/usr" "etcdir=$pkgdir/etc/airoscript" install
  # Fix conflict with extra/screen.
  #mv "$pkgdir/etc/screenrc" "$pkgdir/etc/airoscript/screenrc"
  sed -i 's|/local||' "$pkgdir"/etc/airoscript/*
  sed -i 's|/minimal.theme|defaul.theme|' "$pkgdir"/etc/airoscript/*
}
