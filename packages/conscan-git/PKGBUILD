# Maintainer: ArchAssault <team@archassault.org>
pkgname=conscan-git
pkgver=r13.5746eac
pkgrel=1
groups=('archassault' 'archassault-scanners' 'archassault-fuzzer')
pkgdesc="Concrete5 blackbox vulnerability scanner"
arch=('any')
url='https://github.com/TheXerocouk/conscan'
license=('GPL')
depends=('python2')
makedepends=('git')
provides=('conscan')
replaces=('conscan')
conflicts=('conscan')
source=('git+https://github.com/TheXerocouk/conscan')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/conscan"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

package() {
  cd "$srcdir/conscan"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/conscan"
  install -dm755 "$pkgdir/usr/bin"

  cp -R --no-preserve=ownership data modules "$pkgdir/usr/share/conscan"
  chmod -R 644 "$pkgdir/usr/share/conscan"
  chmod -R a+X "$pkgdir/usr/share/conscan"

  install -m755 conscan.py "$pkgdir/usr/share/conscan"

  cat > "$pkgdir/usr/bin/conscan" <<EOF
cd /usr/share/conscan
./conscan.py "\$@"
EOF
chmod +x "$pkgdir/usr/bin/conscan"
}
