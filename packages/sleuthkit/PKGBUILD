# Maintainer: ArchAssault <team archassault org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=sleuthkit
pkgver=4.1.3
pkgrel=3
epoch=2
pkgdesc='File system and media management forensic analysis tools'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.sleuthkit.org/sleuthkit"
license=('GPL2' 'CPL' 'custom:"IBM Public Licence"')
depends=('perl' 'java-environment' 'afflib' 'libewf')
makedepends=('libewf')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz https://googledrive.com/host/0B3fBvzttpiiScUxsUm54cG02RDA/tsk4.1.3_external_type.patch)
sha256sums=('67f9d2a31a8884d58698d6122fc1a1bfa9bf238582bde2b49228ec9b899f0327'
            'fd96985755e2bfafe7f2a7ce87a86f8a7f7fd258f4a3854ce7b0b18c16b33bfc')

prepare(){
  cd ${pkgname}-${pkgver}/tsk/img
  patch < $srcdir/tsk4.1.3_external_type.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

#licenses
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm0644 licenses/* "${pkgdir}"/usr/share/licenses/${pkgname}
}
