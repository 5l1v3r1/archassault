# Maintainer: ArchAssault <team at archassault org>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>
# Contributor: Florian Hahn <flo@fhahn.com>

_pkgname=toxic
pkgname=${_pkgname}-git
_pkgver=5a0d9f5
pkgver=r1400.5a0d9f5
pkgrel=1
pkgdesc="An ncurses-based commandline client for Tox"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/Tox/toxic"
license=('GPL3')
depends=('tox-git=1:r2905.d8c5be8-1' 'desktop-file-utils' 'freealut' 'libconfig' 'libnotify')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")
install="${pkgname}.install"

source=("${_pkgname}::git+${url}.git#commit=${_pkgver}"
        "${_pkgname}.desktop"
        "${_pkgname}.png")
sha512sums=('SKIP'
            'b75913ecf0cd43f7dccf4c34fdb291f44d9b916909ac93145f2ccf63aa80d15597657c6c0919e90c6d5cf34ed22ad61abfeb9d5f60ee3afc40ac090bf428d51e'
            '13034bbb3b02beb4ac2bdbf27ab2d42a12e00d526367a4bfeec14f1bd561e8148493d562dd91ec03db963e104bb3ea640187840ba6aa856053a7bc3796b41eb6')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${_pkgname}/build
    make
}

package() {
    install -Dm644 ${_pkgname}.desktop "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
    install -Dm644 ${_pkgname}.png "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
    cd ${_pkgname}/build
    make PREFIX=/usr MANDIR=/usr/share/man DESTDIR="$pkgdir" install
}
