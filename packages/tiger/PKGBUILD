# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
pkgname=tiger
pkgver=3.2.3
pkgrel=1
groups=('archassault' 'archassault-scanners' 'archassault-autonomous')
pkgdesc="A security scanner, that checks computer for known problems. Can also use tripwire, aide and chkrootkit."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.nongnu.org/tiger/'
license=('GPL')
depends=('perl')
makedepends=('patch')
backup=('opt/tiger/config' 'etc/tiger/tigerrc' 'etc/tiger/cronrc')
source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz \
        $pkgname-makefile.diff)
sha512sums=('d0b8326ff0a0159aa99e794f114769c56d520c35a48c16d53d2ca90132ac32614fe84ae661f9ad61aa943ce8171b6051a0495e838e19447e1c34f26d5b730d73'
            '79d9d514ee6ce8a30e68fb6ea6c751d21454321cbb96134dc65e66e34edc6c535b40068bc9138140742be0134f2e1961f3425af352caeab34b3363b4b90accde')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np0 -i ${srcdir}/$pkgname-makefile.diff

  sed -i 's/tempfile`/mktemp`/' util/genmsgidx
  sed -i '/explain.idx/s/&&/||/' util/genmsgidx
 
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --with-tigerhome=/usr/share/tiger \
              --with-tigerconfig=/etc/tiger \
              --with-tigerwork=/var/run/tiger \
              --with-tigerlog=/var/log/tiger \
              --with-tigerbin=/usr/bin
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  install -d ${pkgdir}/usr/share/tiger ${pkgdir}/usr/bin
  
  make DESTDIR=${pkgdir} install
}
