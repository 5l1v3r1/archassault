# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=tiger
pkgver=3.2.3
pkgrel=4
groups=('archassault' 'archassault-scanners' 'archassault-autonomous')
pkgdesc="A security scanner that checks computer for known problems"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.nongnu.org/${pkgname}/"
license=('GPL')
depends=('make')
options=('!emptydirs')
install=${pkgname}.install
backup=("etc/${pkgname}/${pkgname}rc"
        "etc/${pkgname}/cronrc")
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-makefile.diff"
        "${pkgname}.tmpfiles.conf"
        "${pkgname}.desktop"
        "${pkgname}.png")
sha512sums=('d0b8326ff0a0159aa99e794f114769c56d520c35a48c16d53d2ca90132ac32614fe84ae661f9ad61aa943ce8171b6051a0495e838e19447e1c34f26d5b730d73'
            '79d9d514ee6ce8a30e68fb6ea6c751d21454321cbb96134dc65e66e34edc6c535b40068bc9138140742be0134f2e1961f3425af352caeab34b3363b4b90accde'
            '787a0db0d4dd16897a4a0e1f7b5095c27d298a4f0b667a633bbdcd660232c46a3cc2f0c95177cdbb1debb6a2b83d77fbc3fbc2ea2757cdfd3360f2fc2dc1707b'
            '712d57cea4281d9d19094056996b956b023480e2d86ab7dc6468a4bd150dc75e85d06f50ac3515c304f092879a7f859e444b6de38159e84b8e61923464084e6e'
            '8207fa992a6e66df9e5f454e4375513e60acba5f4289ae641cc2c494cfe70e46293f865a1c25e9af173fb94489367ef6edc48cd294be73d66edf92c34efd1ea3')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -Np0 -i ../${pkgname}-makefile.diff
    sed -i 's/tempfile`/mktemp`/' util/genmsgidx
    sed -i '/explain.idx/s/&&/||/' util/genmsgidx
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --with-${pkgname}home=/usr/share/${pkgname} \
        --with-${pkgname}config=/etc/${pkgname} \
        --with-${pkgname}work=/var/run/${pkgname} \
        --with-${pkgname}log=/var/log/${pkgname} \
        --with-${pkgname}bin=/usr/bin
    make
}

package() {
    install -Dm644 ${pkgname}.tmpfiles.conf "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
    install -Dm644 ${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
    install -Dm644 ${pkgname}.png "${pkgdir}"/usr/share/pixmaps/${pkgname}.png
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
}
