# Maintainer: ArchAssault <team archassault.org>
pkgname=wphardening
pkgver=1.3
pkgrel=1
pkgdesc="A security tool for WordPress"
arch=('any')
url="https://github.com/elcodigok/wphardening"
groups=('archassault')
license=('GPL3')
depends=('python2')
source=(https://github.com/elcodigok/wphardening/archive/v$pkgver.tar.gz)
sha512sums=('c1de2bff6d8bf8f9b337e34d6dcab81f903b6e44b8bbb04d1f1e204d9e91c1d1114241f67aed715b888b037e9842cbaed052a5ac3ca16262de547f48697538ce')

prepare(){
  grep -iRl 'python' "$srcdir/wphardening-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/$pkgname/data"
  install -dm755 "$pkgdir/usr/share/$pkgname/doc"
  install -dm755 "$pkgdir/usr/share/$pkgname/lib"
  install -dm755 "$pkgdir/usr/share/$pkgname/lib/autoload"
  install -dm755 "$pkgdir/usr/share/$pkgname/lib/command"
  install -Dm644 lib/autoload/*.py "$pkgdir/usr/share/$pkgname/lib/autoload/"
  install -Dm644 lib/command/*.py "$pkgdir/usr/share/$pkgname/lib/command/"
  install -Dm644 README.md "$pkgdir/usr/share/$pkgname/"
  install -Dm644 data/* "$pkgdir/usr/share/$pkgname/data/"
  install -Dm644 AUTHOR "$pkgdir/usr/share/$pkgname/doc/"
  install -Dm644 COPYING "$pkgdir/usr/share/$pkgname/doc/"
  install -Dm644 lib/*.py "$pkgdir/usr/share/$pkgname/lib/"
  install -Dm755 wphardening.py "$pkgdir/usr/share/$pkgname/wphardening.py"

cat > "$pkgdir/usr/bin/$pkgname"<<EOF
#!/bin/sh
cd /usr/share/${pkgname}
python2 ${pkgname}.py "\$@"
EOF

chmod +x $pkgdir/usr/bin/$pkgname
}

