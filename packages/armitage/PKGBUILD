# Maintainer: ArchAssault <team archassault org> 
pkgname=armitage
pkgver=1135
pkgrel=1
groups=('archassault' 'archassault-exploit')
pkgdesc="A graphical cyber attack management tool for Metasploit"
url='http://www.fastandeasyhacking.com/'
arch=('any')
license=('GPL')
depends=('metasploit' 'java-runtime' 'postgresql' 'nmap')
makedepends=('apache-ant' 'zip' 'unzip' 'subversion')
install=armitage.install
source=('svn+http://armitage.googlecode.com/svn/trunk/'
        'armitage.install'
        'armitage.desktop')
sha512sums=('SKIP'
            'c986d01c29de2b8a2b4541433063cbce4ec82a55508bcf74877614ccc9809a8751462b2467aba296a679ff146ba1e6fe60c405cfa18cb6705bc2f6fd595dd998'
            '514baf2b71846878fdbf230eb87ff47f73dbc43bc1d3b61ee76bb3fb6aa25895139bb291c8bb99bd010527a8e991080236f2031221b7e360d9a8d81614ccff97')

pkgver() {
  cd trunk
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$srcdir/trunk"
  sed -i 's|/usr/share/java/apache-ant/bin/||g' package.sh
}

build() {
  cd "$srcdir/trunk"
  bash package.sh
}

package() {
  cd "$srcdir/trunk"
  install -dm755 "$pkgdir/usr/share"
  install -dm755 "$pkgdir/usr/bin"
  cat > "$pkgdir/usr/bin/armitage" <<\EOF
#!/bin/sh
java -jar /usr/share/armitage/armitage.jar "$@"
EOF
  chmod +x "$pkgdir/usr/bin/armitage"
  tar -xzf "$srcdir/trunk/armitage.tgz" -C "$pkgdir/usr/share"
  install -Dm644 "$srcdir/armitage.desktop" "$pkgdir/usr/share/applications/armitage.desktop"
  chown -R root:root "$pkgdir"/*
}
