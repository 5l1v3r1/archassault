# Maintainer: ArchAssault <team@archassault.org>
pkgname=python2-selenium
pkgver=2.46.0
pkgrel=1
pkgdesc="Python binding for Selenium Remote Control [Python 2]"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
depends=('python2')
makedepends=('python2-setuptools')
optdepends=('lib32-glibc: Firefox x86 webdriver support for x86_64')
source=("http://pypi.python.org/packages/source/s/selenium/selenium-${pkgver}.tar.gz")
sha512sums=('58d2bbc2768ccee7c238ed64c0e73d6e56977581ebda5d71a58dfd262e6b724501b49cac11a56fc79b700810ac8f7e09d968f4135f6aed4abf8147eecdce99ea')

build() {
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
   -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
   -e "s|#![ ]*/bin/python$|#!/usr/bin/python2|" \
   $(find $pkgdir -name '*.py')
}
