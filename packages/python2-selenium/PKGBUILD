# Maintainer: ArchAssault <team@archassault.org>
pkgname=python2-selenium
pkgver=2.38.4
pkgrel=2
pkgdesc="Python binding for Selenium Remote Control [Python 2]"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
depends=('python2')
makedepends=('python2-setuptools')
optdepends=('lib32-glibc: Firefox x86 webdriver support for x86_64')
source=("http://pypi.python.org/packages/source/s/selenium/selenium-${pkgver}.tar.gz")
md5sums=('21bbb47d8e6a50c6d329bd7b79a4354c')

build() {
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  find "$pkgdir" -name "*.py" -exec \
    sed -i '1s/python[[:space:]]*$/python2/' {} \;
}
