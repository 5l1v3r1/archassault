pkgname=airgraph-ng
pkgver=2364
pkgrel=3
pkgdesc="Graphing tool for the aircrack suite"
arch=('any')
url="http://www.aircrack-ng.org"
license=('GPL2')
depends=('graphviz' 'python2')
makedepends=("subversion")
groups=('archassault' 'archassault-misc')
source=('svn+http://svn.aircrack-ng.org/trunk/scripts/airgraph-ng' 'airgraph-ng.desktop')
md5sums=('SKIP'
         'b016312e3c258803359c50acd8b85318')

pkgver() {
  cd airgraph-ng
  svnversion | tr -d [A-z]
}

build() {
  cd "${srcdir}/airgraph-ng"
  python2 setup.py build
}

package() {
  cd "${srcdir}/airgraph-ng"
  install -Dm644 "$srcdir/airgraph-ng.desktop" "$pkgdir/usr/share/applications/airgraph-ng.desktop"
  python2 setup.py install --root="${pkgdir}/" --prefix=/usr
}
