# Maintainer: ArchAssault <team@archassault.org>
_sdkname=android-sdk
_pkgname=build-tools
_droidver=4.3.1
pkgname=${_sdkname}-${_pkgname}-${_droidver/W/w}
_pkgver=r18.1.1
pkgver=$_pkgver
pkgrel=1
pkgdesc="Build tools for the Google Android SDK (aapt, aidl, dexdump, dx, llvm-rs-cc)"
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
options=('!strip')
provides=("${_sdkname}-${_pkgname}")

depends=('java-environment'
         'gcc-libs'
         'zlib'
         'bash')
[[ $CARCH = x86_64 ]] && \
depends=('java-environment'
         'lib32-gcc-libs'
         'lib32-zlib'
         'bash')

source=("http://dl-ssl.google.com/android/repository/${_pkgname}_${_pkgver}-linux.zip")
sha512sums=('145b856cf7d14310c262744ca8e7700d9418a7d0da48dc97b3a24d9029ed5926861f2e1168fb04fe82b06f8b92f23899e059ad4ae06787f777fed9cbf6513fd8')

package() {
    install -Dm644 android-${_droidver}/NOTICE.txt "$pkgdir"/usr/share/licenses/${pkgname}/NOTICE
    echo -e $(grep -e "^Pkg.License=" android-${_droidver}/source.properties) > "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE

    install -d "${pkgdir}/etc/profile.d"
    echo "export PATH="'$PATH'":/opt/${_sdkname}/${_pkgname}" > "$pkgdir"/etc/profile.d/${pkgname}.sh
    echo "setenv PATH "'${PATH}'":/opt/${_sdkname}/${_pkgname}" > "$pkgdir"/etc/profile.d/${pkgname}.csh
    chmod 755 "$pkgdir"/etc/profile.d/${pkgname}.{csh,sh}

    install -dm775 "$pkgdir"/opt/${_sdkname}/${_pkgname}
    cp -a android-${_droidver} "$pkgdir"/opt/${_sdkname}/${_pkgname}/${pkgver}
    chmod -R ugo+rX "$pkgdir"/opt/${_sdkname}/${_pkgname}
    chmod ug+w "$pkgdir"/opt/${_sdkname} "$pkgdir"/opt/${_sdkname}/${_pkgname}
}
