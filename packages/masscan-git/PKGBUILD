# Maintainer: ArchAssault <team@archassault.org>
pkgname=masscan-git
pkgver=20141013.r388
pkgrel=1
epoch=1
groups=('archassault' 'archassault-scanners' 'archassault-cracker')
pkgdesc="TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/robertdavidgraham/masscan'
license=('AGPL')
depends=('libpcap')
makedepends=('git')
conflicts=('masscan')
provides=('masscan')
replaces=('masscan')
source=("${pkgname}::git+http://github.com/robertdavidgraham/masscan.git"
		'masscan.desktop')
sha512sums=('SKIP'
			'634257335408b2ad1f3135660fa51b72d4a8363d53db36778a23157e06ac793dc5ef52aeae04d9d4925d9e2de24f0a61be7e888cd96d2a7aba70c9d081e5908a')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "${pkgname}"
  make
}

package() {
  cd "${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/man/man8"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m755 bin/masscan "${pkgdir}/usr/bin"
  gzip -c doc/masscan.8 > "${pkgdir}/usr/share/man/man8/masscan.8.gz"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 "${srcdir}"/masscan.desktop "${pkgdir}/usr/share/applications/masscan.desktop"
}
