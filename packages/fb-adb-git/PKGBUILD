# Maintainer: ArchAssault <team at archassault org>

_pkgname=fb-adb
pkgname=${_pkgname}-git
pkgver=20140815.r70.8367d9b
pkgrel=2
pkgdesc="A better shell to use in place of adb when connecting to Android devices"
url="https://github.com/facebook/${_pkgname}"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('android-tools')
makedepends=('git' 'android-ndk' 'prelink' 'zip')
options=('!strip' '!buildflags')
install=${pkgname}.install

source=("git+${url}.git#branch=master")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    # Detect android-ndk if not currenly in $(env)
    if [[ -z "$ANDROID_NDK" ]]; then
        if [[ -d "/opt/android-ndk" ]]; then
            export ANDROID_NDK=/opt/android-ndk
        elif [[ -d "/opt/android-ndk32" ]]; then
            export ANDROID_NDK=/opt/android-ndk32
        elif [[ -d "/opt/android-ndk64" ]]; then
            export ANDROID_NDK=/opt/android-ndk64
        else
            echo 'Error: cannot find android-ndk'
            return 1
        fi
    fi

    cd $_pkgname
    ./autogen.sh
    mkdir build
    cd build
    ../configure
    make
    execstack -c ${_pkgname}
    cd stub-arm
    zip ${_pkgname}-android-arm.zip ${_pkgname}
}

package() {
    install -Dm755 ${_pkgname}/build/${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
    install -Dm755 ${_pkgname}/build/stub-arm/${_pkgname}-android-arm.zip "${pkgdir}/usr/share/${_pkgname}/${_pkgname}-android-arm.zip"
}
