#Maintainer: ArchAssault <team archassault org>
pkgname=powersploit-git
pkgver=r217.67ed1d7
pkgrel=1
groups=('archassault' 'archassault-windows' 'archassault-exploitation')
pkgdesc="A PowerShell Post-Exploitation Framework"
url="https://github.com/mattifestation/PowerSploit"
arch=(any)
license=('custom')
makedepends=('git')
source=("git+https://github.com/mattifestation/PowerSploit.git")
provides=('powersploit')
replaces=('powersploit')
conflicts=('powersploit')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/PowerSploit"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


package() {
  cd "$srcdir/PowerSploit"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/powersploit"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/powersploit-git/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/powersploit"
}
