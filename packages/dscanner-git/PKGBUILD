# Maintainer: ArchAssault <team archassault org>
pkgname=dscanner-git
pkgver=r661.d610dc2
pkgrel=1
pkgdesc="Swiss-army knife for D source code"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/Hackerpilot/Dscanner"
license=('custom')
depends=('glibc')
groups=('archassault')
makedepends=('git' 'libphobos-devel' 'dmd')
provides=('dscanner')
conflicts=('dscanner')
options=('!strip')
source=("git+https://github.com/Hackerpilot/Dscanner.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/Dscanner"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


prepare() {
    cd "$srcdir/Dscanner"
    git submodule init
    git submodule update
}

build(){
  cd "$srcdir/Dscanner"
  make
}

package() {
  cd "$srcdir/Dscanner"
  install -Dm755 bin/dscanner "$pkgdir/usr/bin/dscanner"
  install -Dm644 LICENSE_1_0.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "$pkgdir"/usr/share/dscanner-git
}
