# Maintainer: ArchAssault <team@archassault.org>
pkgname=suricata
pkgver=1.4.7
pkgrel=3
pkgdesc="An Open Source Next Generation Intrusion Detection and Prevention Engine"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="http://openinfosecfoundation.org/index.php/download-suricata"
license=('GPL2')
groups=('archassault')
depends=('libcap-ng' 'libnet' 'libpcap' 'libyaml' 'pcre')
optdepends=('snort: suricata can use rulesets provided by snort')
backup=('etc/suricata/suricata.yaml')
source=(http://openinfosecfoundation.org/download/$pkgname-$pkgver.tar.gz)
sha256sums=('ae4a117d23a8a304f6fba9bd5b7442422b64e4d6bf5ac1e17312b8ee443a6bc9')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc/$pkgname
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/var/log/$pkgname

  install -D -m644 $pkgname.yaml "${pkgdir}/etc/$pkgname/$pkgname.yaml"
  install -D -m644 classification.config "${pkgdir}/etc/$pkgname/classification.config"
  install -D -m644 reference.config "${pkgdir}/etc/$pkgname/reference.config"
 
  install -d "${pkgdir}/etc/$pkgname/rules"
  install -D -m644 rules/*.rules "${pkgdir}/etc/$pkgname/rules/"
}
