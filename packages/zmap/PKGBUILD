# Maintainer: ArchAssault <team@archassault.org>
pkgname=zmap
pkgver=1.2.1.RC1
pkgrel=1
groups=('archassault' 'archassault-scanners')
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
license=('Apache')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('gmp' 'libpcap' 'json-c')
makedepends=('byacc-noconflict' 'cmake' 'gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=("https://github.com/zmap/zmap/archive/v1.2.1-RC1.tar.gz")
sha512sums=('da7f5b908dafc33017bbd1b0c02678255713b8d2fb4d19f09eed451f93cc8bf9208c96b6a3a5c2ad7b5bc2f936521d717174062f968674438269664383852068')

build() {
  cd "$srcdir/zmap-1.2.1-RC1"

  cmake -DWITH_JSON=ON
  make
}

package() {
  cd "$srcdir/zmap-1.2.1-RC1/src"

  # Bin.
  install -Dm755 zmap "$pkgdir/usr/bin/zmap"

  # Man.
  install -Dm644 zmap.1 "$pkgdir/usr/share/man/man1/zmap.1"

  # Etc.
  install -dm755 "$pkgdir/etc/zmap"
  install -m644 ../conf/* "$pkgdir/etc/zmap"

  # Doc.
  cd ..
  for f in AUTHORS CHANGELOG INSTALL README; do
    install -Dm644 $f "$pkgdir/usr/share/doc/zmap/$f";
  done
  cp -a examples "$pkgdir/usr/share/doc/zmap"
}
