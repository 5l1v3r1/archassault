# Maintainer: ArchAssault <team archassault org>
pkgname=ruby1.9
pkgdesc='An object-oriented language for quick and easy programming'
pkgver=1.9.3_p545
pkgrel=1
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.ruby-lang.org/en/'
license=('BSD' 'custom')
depends=('openssl' 'libffi' 'libyaml')
optdepends=('tk: for Ruby/TK')
makedepends=('openssl' 'tk' 'libffi' 'doxygen' 'graphviz' 'libyaml')
options=('!emptydirs' '!makeflags')
provides=('ruby1.9-gems')
source=("ftp://ftp.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver//_/-}.tar.bz2")
sha512sums=('7fbdaac5950abcb3cc9a2f99f441eced5f2a7d0c9107a41fb4b6caf299e372edab2be24a294ec1a6d6863b865f9cb5511ca1c15ec6bb1f45e60ffa7ab5480e04')

build() {
  cd "ruby-${pkgver//_/-}"

if [[ $CARCH == arm* ]] ; then
  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr/share/ruby1.9 \
    --enable-shared \
    --enable-pthread \
    --disable-rpath \
    --disable-install-doc \
    --disable-install-rdoc
else
  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr/share/ruby1.9 \
    --enable-shared \
    --enable-pthread \
    --disable-rpath
fi

  make
}

check() {
  cd "ruby-${pkgver//_/-}"

  make test
}

package() {
  cd "ruby-${pkgver//_/-}"

  make DESTDIR="$pkgdir" install-nodoc

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/lib"

  # Bin.
  for i in erb irb rdoc ri ruby testrb rake gem; do
    ln -s "/usr/share/ruby1.9/bin/$i" "$pkgdir/usr/bin/$i-1.9"
  done

  # Lib.
  ln -s "/usr/share/ruby1.9/lib/libruby.so.1.9" "$pkgdir/usr/lib/libruby.so.1.9"

  # License.
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/ruby1.9/LICENSE"
  install -D -m644 BSDL "$pkgdir/usr/share/licenses/ruby1.9/BSDL"
}
