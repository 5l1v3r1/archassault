# Maintainer: ArchAssault <team archassault org>
# Contributor: Jonne HaÃŸ <me@mrzyx.de>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>
pkgname=ruby1.9
pkgdesc='An object-oriented language for quick and easy programming'
pkgver=1.9.3_p547
pkgrel=3
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://www.ruby-lang.org/en/'
license=('BSD' 'custom')
depends=('openssl' 'libffi' 'libyaml')
optdepends=('tk: for Ruby/TK')
makedepends=('openssl' 'tk' 'doxygen' 'graphviz' 'libyaml' 'ttf-liberation')
options=('!emptydirs' '!makeflags')
provides=('ruby1.9-gems')
source=("ftp://ftp.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver//_/-}.tar.bz2")
sha512sums=('d6e166f7a12ffa57d0287f7eaa7cd7717f09a745beb58f2755331874086df1a69c71144cfce5ba00d46cc44da46317fbc0d18f44a7bd65f2b2aaf149331e0d26')

build() {
  cd "ruby-${pkgver//_/-}"
  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr/share/$pkgname \
    --enable-shared \
    --enable-pthread \
    --disable-rpath \
    --program-suffix=-1.9 
  make
}

check() {
  cd "ruby-${pkgver//_/-}"
  make test
}

package() {
  cd "ruby-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install-nodoc
  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/lib"
  # Bin.
  for i in erb irb rdoc ri ruby testrb rake gem; do
    ln -s "/usr/share/ruby1.9/bin/$i-1.9" "$pkgdir/usr/bin/$i-1.9"
    ln -s "/usr/share/ruby1.9/bin/$i-1.9" "$pkgdir/usr/share/$pkgname/bin/$i"
  done
  # Lib.
  ln -s "/usr/share/ruby1.9/lib/libruby.so.1.9" "$pkgdir/usr/lib/libruby.so.1.9"
  # License.
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/ruby1.9/LICENSE"
  install -D -m644 BSDL "$pkgdir/usr/share/licenses/ruby1.9/BSDL"
}
