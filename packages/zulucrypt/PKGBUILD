# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Salan54 <salan at fremenil dot com>

pkgname=zulucrypt
pkgver=4.7.3
pkgrel=1
pkgdesc="a cli and gui frontend to cryptsetup."
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url="https://github.com/mhogomchungu/zuluCrypt/"
license=('GPL')
depends=('cryptsetup' 'qt4' 'libpwquality' 'libsecret' 'tcplay' 'gtk-update-icon-cache')
makedepends=('cmake' 'git' 'automoc4')
optdepends=('kdeutils-kwallet: retrieve volume keys from kde kwallet')
options=('!strip' '!buildflags' '!distcc')
source=("https://github.com/mhogomchungu/zuluCrypt/archive/${pkgver}.zip")
sha512sums=('31cc9547ea50907eb0fee02199a00c5b52af86231bafe8d4db0355d74094829f2a03c1edcd48723633da8c5c01cd87a2bdc20ba63aa04fca3f3395388f548d0a')
install=${pkgname}.install

build() {
    mkdir "${srcdir}"/zuluCrypt-"${pkgver}"/build
    cd "zuluCrypt-${pkgver}/build"
    cmake -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DNOKDE=true -DUDEVSUPPORT=true \
    -DCMAKE_BUILD_TYPE=release \
    -DCMAKE_LIB_SUFFIX=/usr/lib . ..
    make
}
 
package() {
  cd "zuluCrypt-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  chown root:root "${pkgdir}/usr/bin/zuluCrypt-cli"
  chown root:root "${pkgdir}/usr/bin/zuluMount-cli"
  chmod 0755 "${pkgdir}/usr/bin/zuluMount-cli"
  chmod 0755 "${pkgdir}/usr/bin/zuluCrypt-cli"
  if [[ -d "${pkgdir}"/usr/lib64 ]] ; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}
