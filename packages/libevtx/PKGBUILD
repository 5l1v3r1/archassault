# Maintainer: ArchAssault <team archassault org>
pkgname="libevtx"
pkgver="20141229"
pkgrel=1
pkgdesc="Library and tooling to access the Windows XML Event Log (EVTX) format"
arch=("i686" "x86_64" "armv6h" "armv7h")
url='https://github.com/libyal/libevtx'
license=('GPL3')
depends=('libcsystem' 'libbfio' 'python2')
makedepends=('git')
source=("https://github.com/libyal/libevtx/archive/$pkgver.tar.gz")
sha512sums=('e9f972cbed4493ce7cd6de2c220c1fa3f9836feeeb458f69e28d577fa56e02b2123b4b28f43bd090866b23a9316dc4bda98e9d4d2e2c45e3545e9fc3d00a5325')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./synclibs.sh
  ./autogen.sh
  PYTHON_VERSION="2" ./configure --prefix=/usr --disable-rpath --enable-verbose-output --enable-debug-output --enable-python
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
