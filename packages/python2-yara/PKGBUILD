# Maintainer: ArchAssault <team archassautl org>
# Contributor: Th√©ophile Studer

_pkgname=yara
pkgname=python2-${_pkgname}
pkgver=3.3.0
pkgrel=1
groups=('archassault' 'archassault-malware')
pkgdesc="A malware identification and classification tool"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://plusvic.github.io/${_pkgname}"
license=('APACHE')
replaces=("${_pkgname}-python")
depends=("python2" "${_pkgname}")
source=("https://github.com/plusvic/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f763f4d5b71caf5aef20995d128e1e16f176746dd2fade1f3991a7a81e050ba8c0d9aa1d96bb4dc754e5f0a44fa799d620c5f4797bcd3205fad77f9c493c5532')

prepare() {
    cd ${_pkgname}-${pkgver}/${_pkgname}-python
    sed -i 's|env python$|env python2|' *.py
}

build() {
    cd ${_pkgname}-${pkgver}/${_pkgname}-python
    python2 setup.py build
}

package() {
    cd ${_pkgname}-${pkgver}/${_pkgname}-python
    python2 setup.py install --root="$pkgdir" --optimize=1
}
