# Maintainer: ArchAssault <team@archassault.org>
pkgname=dirs3arch
pkgver=0.2.4
pkgrel=1
groups=('archassault' 'archassault-webapps')
pkgdesc="A HTTP(S) directory/file brute forcer"
arch=('any')
url='https://github.com/maurosoria/dirs3arch'
license=('custom')
depends=('python2')
source=("https://github.com/maurosoria/dirs3arch/archive/v$pkgver.tar.gz")
sha512sums=('f8898231bf79f26a64334ea79b4c18105546fc8b92ea68e99187e088ecc75a1dd309b33a7a727c0cd7bfe113b7a84a827d012a353b165872817d7f4931a4f06e')

prepare(){
  grep -iRl 'python' "$srcdir/dirs3arch-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "$srcdir/dirs3arch-$pkgver"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/dirs3arch"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  for i in db lib thirdparty; do install -dm755 "$pkgdir/usr/share/$pkgname/$i"; cp -a --no-preserve=ownership $i/* "$pkgdir/usr/share/$pkgname/$i/";done
  install -Dm755 dirs3arch.py "$pkgdir/usr/share/$pkgname"
  install -Dm755 config.py "$pkgdir/usr/share/$pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
 
 
cat > "$pkgdir/usr/bin/dirs3arch" << EOF
#!/bin/sh
cd /usr/share/dirs3arch
python2 dirs3arch.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/dirs3arch"

}
