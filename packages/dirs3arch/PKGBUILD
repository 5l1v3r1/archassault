# Maintainer: ArchAssault <team@archassault.org>
pkgname=dirs3arch
pkgver=0.2.6
pkgrel=1
groups=('archassault' 'archassault-webapps')
pkgdesc="A HTTP(S) directory/file brute forcer"
arch=('any')
url='https://github.com/maurosoria/dirs3arch'
license=('custom')
depends=('python2')
source=("https://github.com/maurosoria/dirs3arch/archive/v$pkgver.tar.gz")
sha512sums=('e91733dc50693e706cce2bfaeab296bb27975696872729f716058017148c9f0e21aa66ea1c4f08a9f8717596a532482e4c9e89d05643de1b194f7e80496d98a5')

prepare(){
  grep -iRl 'python' "$srcdir/dirs3arch-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "$srcdir/dirs3arch-$pkgver"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/dirs3arch"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  for i in db lib thirdparty reports; do install -dm755 "$pkgdir/usr/share/$pkgname/$i"; cp -a --no-preserve=ownership $i/* "$pkgdir/usr/share/$pkgname/$i/";done
  install -Dm755 dirs3arch.py "$pkgdir/usr/share/$pkgname"
  install -Dm755 default.conf "$pkgdir/usr/share/$pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
 
 
cat > "$pkgdir/usr/bin/dirs3arch" << EOF
#!/bin/sh
cd /usr/share/dirs3arch
python2 dirs3arch.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/dirs3arch"

}
