# Maintainer: ArchAssault <team@archassault.org>
pkgname=scanmem
pkgver=0.15.1
pkgrel=1
groups=('archassault' 'archassault-reverse')
pkgdesc="A utility used to locate the address of a variable in an executing process."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/scanmem'
license=('GPL3')
depends=('pygtk' 'desktop-file-utils') 
makedepends=('intltool')
optdepends=('gksu: for the GameConqueror GUI.')
install=${pkgname}.install
source=("https://github.com/scanmem/scanmem/archive/v${pkgver}.tar.gz")
sha512sums=('a78ea7bc4a0f3ee068a7b9de3efb3a3db734ae2280bd6a270be947cc6a8254ff715e65ff007a8593be04030b9d29d420dd1564fd57349fc61a4f99da004b10e4')

build() {
  cd "$srcdir/scanmem-$pkgver"
  ./autogen.sh
  chmod 755 configure
  ./configure --enable-gui --prefix=/usr
  make
}

package() {
  cd "$srcdir/scanmem-$pkgver"

  make -s DESTDIR="$pkgdir" install

  # Fix Python shebang lines.
  sed 's|python|python2|g' -i "$pkgdir/usr/bin/gameconqueror"
  sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i \
      "$pkgdir/usr/share/gameconqueror/"{backend.py,GameConqueror.py,hexview.py}
}

