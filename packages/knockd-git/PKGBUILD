# Maintainer: ArchAssault <team archassault org>
# Contributor: Michael Goehler <somebody.here@gmx.de>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contriburor: Steven <steven at stebalien dot com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=knockd-git
_pkgname=knock
pkgver=r27.7ce33cd
pkgrel=1
pkgdesc="A simple port-knocking daemon and client"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.zeroflux.org/projects/knock"
license=(GPL)
depends=('libpcap')
conflicts=('knockd')
makedepends=(git)
backup=('etc/knockd.conf')
source=('knock::git+https://github.com/jvinet/knock.git'
        'knockd.logrotate'
        'knockd.service')
sha512sums=('SKIP'
            '3ea2ae9392d3d79bb5df6b75090f6c1a9548979e01dcd4a75471cfa46dbc70401a166496b5158f90fc61326a554acacc38f8605aab02b5b53b75d60a7d80648a'
            '5c32e52a2b0d2d3a5c81d8a59e938152207eaa1ce810f648fd35c6444b6a7a5264430721b1205e98edb2457a14ae3f2647855c496a16f2ec7d638adc158a7da8')

pkgver() {
    cd "$srcdir/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/${_pkgname}"
    autoreconf -fi
    ./configure --prefix=/usr \
                --sbin=/usr/bin \
                --sysconfdir=/etc
    make
}

package() {
  cd "$srcdir/${_pkgname}"
  make DESTDIR="$pkgdir" MANDIR=/usr/share/man install
  install -Dm755 "$srcdir/knockd.service" "$pkgdir/usr/lib/systemd/system/knockd.service"
  install -Dm644 "$srcdir/knockd.logrotate" "$pkgdir/etc/logrotate.d/knockd"
}

# vim:set ts=4 sw=4 et:
