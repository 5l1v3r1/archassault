# Maintainer: ArchAssault <team archassault org>
pkgname=pip3line
pkgver=2.5.2
pkgrel=2
groups=('archassault' 'archassault-fuzzer')
pkgdesc="Swiss army knife of byte manipulation"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/nccgroup/pip3line'
license=('GPL3')
if [[ "$CARCH" == 'x86_64' ]]; then
     depends=('qt4' 'python2' 'python' 'gcc-libs-multilib' 'openssl' 'distorm' 'xdg-utils')
else
     depends=('qt4' 'python2' 'python' 'gcc-libs' 'openssl' 'distorm' 'xdg-utils')
fi
makedepends=('cmake')
source=("https://github.com/nccgroup/pip3line/archive/v$pkgver.tar.gz" "py3-pip3line.patch" "pip3line.install" "libdir.patch") 
sha512sums=('8c158c5746eddf5bd6ea6f00e0f9ea0a31ea3905117e59c492c8f277a281982b4492bec6b1fc3c15e4fc13358e0881d2fe6e21c03f6e8104ca4200e00a68033b'
            '48957f4c027db2cae6d4485e7378126f376dbd20ca047b68d270f537bdf3cb3294ceff427e99abed5edb598428cb3994391292e0354ddfc2ff10d9e5585c63c5'
            'ca869745675aceabe2b130ffe69db508ecd7558b47dc909eded0115b8250ffe2e1898ef013c57c33c7dbd6ec7fb31404c5e120be0e59a5cda7bdecb0612c01c8'
            '8904cf09217a990f0ee879ae647f0a62a9953ff860dee7cbe174734cf9980c3a4ce8a013d3d97d29508dd37a4f0893132a0582489f4df5001970864053ab1c30')
install='pip3line.install'

prepare(){
  cd "$srcdir/pip3line-$pkgver"
  patch -p1 -i $srcdir/py3-pip3line.patch
  patch -p1 -i $srcdir/libdir.patch
}

build(){
  cd "$srcdir/pip3line-$pkgver"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_SKIP_RPATH=ON -DALL=ON -DWITH_DISTORM=ON -DWITH_OPENSSL=ON -DWITH_PYTHON27=ON  -DWITH_PYTHON3=ON ../
  make
}

package(){
  cd "$srcdir/pip3line-$pkgver/build"
  make DESTDIR=$pkgdir install
}
