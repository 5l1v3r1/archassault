# Maintainer: ArchAssault <team archassault org>
pkgname=beef
pkgver=20140929.r2179
pkgrel=1
epoch=1
groups=('archassault' 'archassault-exploit')
pkgdesc="BeEF, the Browser Exploitation Framework - it is a penetration testing tool that focuses on the web browser"
arch=(armv6h armv7h i686 x86_64)
url='http://beefproject.com/'
license=('Apache')
makedepends=('git')
options=('!emptydirs')
depends=('ruby1.9' 'java-environment' 'python2' 'sqlite' 'ruby1.9-bundler')
source=('git+http://github.com/beefproject/beef.git')
md5sums=('SKIP')

pkgver() {
    cd "${pkgname}"
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd "${srcdir}/beef"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/beef"
  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/beef"
  cd ${pkgdir}/usr/share/beef
  bundle-1.9 install --path vendor/bundle

  cat > "${pkgdir}/usr/bin/beef" <<EOF
#!/bin/sh
cd /usr/share/beef
ruby-1.9 ./beef "\$@"
EOF
  chmod +x "${pkgdir}/usr/bin/beef"
}
