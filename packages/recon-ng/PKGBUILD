pkgname='reconnaissance-ng'
pkgver=556.05e25d0
pkgrel=3
groups=('archassault' 'archassault-reconnaissance')
pkgdesc='A full-featured Web Reconnaissance framework written in Python.'
arch=('any')
url='https://bitbucket.org/LaNMaSteR53/reconnaissance-ng'
license=('GPL3')
depends=('python2' 'python2-slowaes' 'python2-dnspython' 'python2-jsonrpclib')
source=("git+https://bitbucket.org/LaNMaSteR53/reconnaissance-ng.git"
        "reconnaissance-ng.sh"
        "reconnaissance-cli.sh"
        "reconnaissance-rpc.sh")
md5sums=('SKIP'
         'db9592b02efa88a45b9d4319e4f3c8b2'
         '5fb21c6589dd593036f54c026af88ec3'
         'a54d328beb89f77221b1bb65bcbd3ae6')

pkgver() {
  cd "$srcdir/reconnaissance-ng"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/reconnaissance-ng"
  sed -i '1s/env python/env python2/' "reconnaissance-ng"
  sed -i '1s/env python/env python2/' "reconnaissance-cli"
  sed -i '1s/env python/env python2/' "reconnaissance-rpc"
}

package() {
  cd "$srcdir/reconnaissance-ng"
  install -d -m755 "$pkgdir/usr/bin"
  install -D -m755 "$srcdir/reconnaissance-ng.sh" "$pkgdir/usr/bin/reconnaissance-ng"
  install -D -m755 "$srcdir/reconnaissance-cli.sh" "$pkgdir/usr/bin/reconnaissance-cli"
  install -D -m755 "$srcdir/reconnaissance-rpc.sh" "$pkgdir/usr/bin/reconnaissance-rpc"
  install -d -m755 "$pkgdir/usr/share/reconnaissance-ng"
  cp -R * "$pkgdir/usr/share/reconnaissance-ng"
}
