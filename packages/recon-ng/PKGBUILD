# Maintainer: ArchAssault <team archassault org>
pkgname='recon-ng'
pkgver=796.7f6a07a
pkgrel=1
groups=('archassault' 'archassault-reconnaissance')
pkgdesc='A full-featured Web Reconnaissance framework written in Python.'
arch=('any')
url='https://bitbucket.org/LaNMaSteR53/recon-ng'
license=('GPL3')
depends=('python2' 'python2-slowaes' 'python2-dnspython' 'python2-jsonrpclib')
makedepends=('git')
source=("git+https://bitbucket.org/LaNMaSteR53/recon-ng.git"
        "recon-ng.sh"
        "recon-cli.sh"
        "recon-rpc.sh")
md5sums=('SKIP'
         'db9592b02efa88a45b9d4319e4f3c8b2'
         '5fb21c6589dd593036f54c026af88ec3'
         'a54d328beb89f77221b1bb65bcbd3ae6')

pkgver() {
  cd "$srcdir/recon-ng"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/recon-ng"
  sed -i '1s/env python/env python2/' "recon-ng"
  sed -i '1s/env python/env python2/' "recon-cli"
  sed -i '1s/env python/env python2/' "recon-rpc"
}

package() {
  cd "$srcdir/recon-ng"
  install -d -m755 "$pkgdir/usr/bin"
  install -D -m755 "$srcdir/recon-ng.sh" "$pkgdir/usr/bin/recon-ng"
  install -D -m755 "$srcdir/recon-cli.sh" "$pkgdir/usr/bin/recon-cli"
  install -D -m755 "$srcdir/recon-rpc.sh" "$pkgdir/usr/bin/recon-rpc"
  install -d -m755 "$pkgdir/usr/share/recon-ng"
  cp -R * "$pkgdir/usr/share/recon-ng"
}
