# Maintainer: ArchAssault <team@archassault.org>
pkgname=python2-cybox
pkgver=2.1.0.9
pkgrel=1
pkgdesc="A Python library for parsing and generating CybOX 2.1.0 content."
url="http://cybox.mitre.org"
depends=('python2')
makedepends=('python2-distribute')
license=('custom')
arch=('any')
source=("https://pypi.python.org/packages/source/c/cybox/cybox-${pkgver}.zip")
sha512sums=('8d6a74054ab7a6d7e030bd4c3b2c443e23a083cc5fc914ead316cf6f08dea15bcca238a1f035226da3d79ca619c2c1ae5bbd61f147c407788c8e6f29c61bab92')

build() {
  cd cybox-"${pkgver}"
  python2 setup.py build
}

package() {
  cd cybox-"${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm0644 LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE.txt
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
