# Maintainer: ArchAssault <team@archassault.org>
pkgname=android-sdk-platform-tools
pkgver=r19.0.2
pkgrel=1
pkgdesc="Platform-Tools for Google Android SDK (adb and fastboot)"
groups=('archassault' 'archassault-mobile')
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url='http://developer.android.com/sdk/index.html'
license=('custom')
depends=('gcc-libs' 'python2')
[[ $CARCH = x86_64 ]] && depends=('lib32-gcc-libs' 'python2')
source=("http://dl-ssl.google.com/android/repository/platform-tools_${pkgver}-linux.zip"
        "adb.service"
        "license.html")
sha512sums=('fa64173893ce5ec40bddf19d28f56a986b7645a733456bc42f8089ad4b0b2771eb7a58114ae05ec3329ca1ea133dd18576aafa1afd41585ca0673691643a8f7e'
            'faf315d2bb7a9caa099db32e21451a1fd8fcc76475f25b36742356adc4f758e6fc7512106f9539b2db687c9566ef5889a4fa051c3c1a84555090d4b175e485d4'
            '23f1533682c39e2ae2ae40083de343abcd6ed0a523eacdac58768067f8f450b1cec30073993dd7b972a5b8125136ed29a043ae894dff046fd75bb04c4d21a0f4')

prepare() {
    sed -i 's|/usr/bin/env python|/usr/bin/env python2|' platform-tools/systrace/*.py
}

package() {
    install -Dm644 adb.service "${pkgdir}/usr/lib/systemd/system/adb.service"
    install -Dm644 license.html "${pkgdir}/usr/share/licenses/android-sdk-platform-tools/license.html"

    install -d "${pkgdir}/etc/profile.d"
    echo 'export PATH=$PATH:/opt/android-sdk/platform-tools' > "${pkgdir}/etc/profile.d/android-sdk-platform-tools.sh"
    echo 'setenv PATH ${PATH}:/opt/android-sdk/platform-tools' > "${pkgdir}/etc/profile.d/android-sdk-platform-tools.csh"
    chmod 755 "${pkgdir}/etc/profile.d/android-sdk-platform-tools."{csh,sh}

    install -d "${pkgdir}/opt/android-sdk"
    cp -a platform-tools "${pkgdir}/opt/android-sdk/platform-tools"
    chmod -R ugo+rX "${pkgdir}/opt"
    chmod g+rwX "${pkgdir}/opt/android-sdk/platform-tools"
}
