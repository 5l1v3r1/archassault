# Maintainer: ArchAssault <team@archassault.org>
_sdkname=android-sdk
_pkgname=build-tools
_droidver=4.4.2
pkgname=${_sdkname}-${_pkgname}-${_droidver/W/w}
_pkgver=r19.1
pkgver=${_pkgver}.0
pkgrel=3
pkgdesc="Build tools for the Google Android SDK (aapt, aidl, dexdump, dx, llvm-rs-cc)"
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
options=('!strip')
provides=("${_sdkname}-${_pkgname}")

_depends=('java-environment'
          'lib32-gcc-libs'
          'lib32-zlib'
          'bash')
[[ ! "$CARCH" = "x86_64" ]] \
    && _depends=(${_depends[@]/lib32-}) \
    || _makedepends+=('gcc-multilib')
depends=(${_depends[@]})
makedepends=(${_makedepends[@]})

source=("http://dl-ssl.google.com/android/repository/${_pkgname}_${_pkgver}-linux.zip")
sha512sums=('a9ecf01e17cacda0ef3797a44377b310ca511d371177ae221a4dc986317433d88f78f63670388acc4f16b73c3df509798c34d489d8ab5a761652ee8eb9ee581d')

package() {
    install -Dm644 android-${_droidver}/NOTICE.txt "$pkgdir"/usr/share/licenses/${pkgname}/NOTICE
    echo -e $(grep -e "^Pkg.License=" android-${_droidver}/source.properties) > "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE

    install -dm775 "$pkgdir"/opt/${_sdkname}/${_pkgname}
    cp -a android-${_droidver} "$pkgdir"/opt/${_sdkname}/${_pkgname}/${pkgver/r}
    chmod -R ugo+rX "$pkgdir"/opt/${_sdkname}/${_pkgname}
    chmod ug+w "$pkgdir"/opt/${_sdkname} "$pkgdir"/opt/${_sdkname}/${_pkgname}
}
