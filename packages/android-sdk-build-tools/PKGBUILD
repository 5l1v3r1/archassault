# Maintainer: ArchAssault <team@archassault.org>
_sdkname=android-sdk
_pkgname=build-tools
pkgname=${_sdkname}-${_pkgname}
pkgver=r20
_droidver=android-4.4W
pkgrel=1
pkgdesc="Build tools for the Google Android SDK (aapt, aidl, dexdump, dx, llvm-rs-cc)"
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
options=('!strip')

depends=('java-environment'
         'gcc-libs'
         'zlib'
         'bash')
[[ $CARCH = x86_64 ]] && \
depends=('java-environment'
         'lib32-gcc-libs'
         'lib32-zlib'
         'bash')

source=("http://dl-ssl.google.com/android/repository/${_pkgname}_${pkgver}-linux.zip")
sha512sums=('4f21e1884efe95ac6531419ac4fa198a70494d6a20cfd0e1ca320dd21cd2ae1639d4e3d35fc96e97c5afd1e09eb99068ed9723899cf27661bf0eed04a940172e')

package() {
    install -Dm644 ${_droidver}/NOTICE.txt "${pkgdir}/usr/share/licenses/${pkgname}/NOTICE.txt"

    install -d "${pkgdir}/etc/profile.d"
    echo "export PATH="'$PATH'":/opt/${_sdkname}/${_pkgname}" > "${pkgdir}/etc/profile.d/${pkgname}.sh"
    echo "setenv PATH "'${PATH}'":/opt/${_sdkname}/${_pkgname}" > "${pkgdir}/etc/profile.d/${pkgname}.csh"
    chmod 755 "${pkgdir}/etc/profile.d/${pkgname}."{csh,sh}

    install -dm775 "${pkgdir}/opt/${_sdkname}"
    cp -a ${_droidver} "${pkgdir}/opt/${_sdkname}/${_pkgname}"
    chmod -R ugo+rX "${pkgdir}/opt/${_sdkname}/${_pkgname}"
    chmod g+rwX "${pkgdir}/opt/${_sdkname}/${_pkgname}"
}
