# Maintainer: ArchAssault <team archassault org>

pkgname=american-fuzzy-lop
pkgver=1.71b
pkgrel=1
groups=('archassault' 'archassault-fuzzers')
pkgdesc="A practical, instrumentation-driven fuzzer for binary formats"
arch=('i686' 'x86_64' 'armv7h')
url="http://lcamtuf.coredump.cx/afl/"
license=('APACHE')
depends=('bash')
options=('!strip')
source=("http://lcamtuf.coredump.cx/afl/releases/afl-${pkgver}.tgz" "makefile.patch")
sha512sums=('cbc5d22c9b3c477d1bad6c757267728c6530ab9b55a6b1e413f5f209250cd95fc38cc5b438215e8a05627ec551e98220372db64357124ffeaaacc0bdef2245da'
            '33e8838fb4f9dfc312753dcc59e94086abf26607614a6bf31ec0a5fe06d891f84d931766f9f07b94b29cdefe6b8e736cb4ab3b1ce4f9af4148bc750de279c485')

prepare(){
 cd "$srcdir/afl-$pkgver"
 patch < $srcdir/makefile.patch
}

build() {
  cd "$srcdir/afl-$pkgver"
  if [[ $CARCH == "armv7h" ]] || [[ $CARCH == "armv6h" ]]; then
     cp  experimental/arm_support/*.c .
     cp  experimental/arm_support/*.h .
     sed -i '/__asm__/d' Makefile
     export CFLAGS='-Wcpp -fstack-protector-strong'
     make
  else
     make
  fi
}

package() {
  cd "$srcdir/afl-$pkgver"
  install -dm755 $pkgdir/usr/bin
  install -dm755 $pkgdir/usr/lib/afl
  if [[ $CARCH == "armv7h" ]] || [[ $CARCH == "armv6h" ]]; then
        export CFLAGS='-Wcpp -fstack-protector-strong'
        make DESTDIR=$pkgdir install
  else
        make DESTDIR=$pkgdir install
  fi
}
