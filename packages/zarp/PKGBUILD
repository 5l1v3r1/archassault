# Maintainer: ArchAssault <team@archassault.org>
pkgname=zarp
pkgver=0.1.5
pkgrel=2
groups=('archassault' 'archassault-networking' 'archassault-exploits')
pkgdesc="A network attack tool centered around the exploit of local networks."
url="https://github.com/hatRiot/zarp"
arch=('any')
license=('GPL')
depends=('python2' 'python2-paramiko' 'python2-netlib' 'python2-pyasn1' 'python2-flask')
optdepends=('aircrack-ng: aiding wireless attacks')
source=("https://github.com/hatRiot/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('650b371a0a088b61e3a1d4783e68a08af599c6f687445324f3fdd7c3c0a097651757f8de00844e9014998b91183e675cebfac9835f7df65ed0255b0e9fb0ba79')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Update to use python2
  grep '#!/usr/bin/python' * -Rl | xargs -I{} sed -i 's/#!\/usr\/bin\/python/#!\/usr\/bin\/python2/g' {}
  grep '#! /usr/bin/python' * -Rl | xargs -I{} sed -i 's/#! \/usr\/bin\/python/#!\/usr\/bin\/python2/g' {}
  grep '#!/usr/bin/env python' * -Rl | xargs -I{} sed -i 's/#!\/usr\/bin\/env python/#!\/usr\/bin\/python2/g' {}
  grep '#! /usr/bin/env python' * -Rl | xargs -I{} sed -i 's/#! \/usr\/bin\/env python/#!\/usr\/bin\/python2/g' {}


  # Base directories.
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/zarp"

  cp -a * "${pkgdir}/usr/share/zarp"

  cat > "${pkgdir}/usr/bin/zarp" << EOF
#!/bin/sh
cd /usr/share/zarp
python2 zarp.py "\$@"
EOF
  chmod +x "${pkgdir}/usr/bin/zarp"
}
