# Maintainer: ArchAssault <team archassault org>
pkgname=binwalk
pkgver=1.3.0
pkgrel=2
pkgdesc="A tool for searching a given binary image for embedded files"
arch=('armv6h' 'i686' 'armv7h' 'x86_64')
url="http://binwalk.org"
groups=('archassault')
license=('MIT')
depends=('python2-magic' 'python2-pyqtgraph' 'python2-pydeep' 'python2-opengl')
makedepends=('python2-setuptools')
optdepends=('firmware-mod-kit: Firmware extract rules helper'
		'git' 'mtd-utils' 'zlib' 'python2-lzma' 'ncompress' 'gzip' 'bzip2' 'tar'
		'arj' 'p7zip' 'jdk7-openjdk')
source=(https://github.com/devttys0/${pkgname}/archive/v${pkgver}.tar.gz ${pkgname}.patch)
install=binwalk.install
sha512sums=('71b5d80e2e4219f3ee43cf360d92452f33379bec99849fba9189b0c1caee3ee9087d5c47098fa27b326109e756c7792e90b7f9a03c2dac4d103a1a65c47a4c8c'
            '15726b805960bb4b2a9f95d098f36cc42cb5767ae653fc3b9eb30350198b0407536755ea4dd772c949d9b0ad73e53bed5e96cb14e71fa1613da0f9200a35f939')

prepare() {
	cd "${srcdir}"
		patch -p0 < binwalk.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}/src"
		python2 setup.py build
		cd C
		./configure --prefix=/usr
		make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/src"
		cd C
		make DESTDIR="${pkgdir}" install
		cd ..
		python2 setup.py install --root="${pkgdir}"
		install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
		chmod a+x "${pkgdir}"/usr/lib/lib*
}

