# Maintainer: ArchAssault <team archassault org>
# Contributor: William Robertson <nullptr@gmail.com>
pkgname=silk
pkgver=3.10.0
pkgrel=1
pkgdesc='CERT netflow tools.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://tools.netsa.cert.org/silk'
license=('GPL')
groups=('archassault' 'archassault-networking')
depends=('libfixbuf' 'lzo2' 'libpcap' 'python2' 'gnutls' 'c-ares' 'adns')
makedepends=('perl' 'flex' 'bison')
install="$pkgname.install"
source=("http://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('91e54018477cc7e01ec59dd3650c4020ff0392cd21cdc6c7485ac5a0d6f68a908afe854736d3ed52de1bb44e9bb23d29cea38685253cbce49e9510798025560c')

prepare(){
  grep -iRl 'python' "$srcdir/$pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


build() {
	cd "$srcdir/$pkgname-$pkgver"
        ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6 --with-python=$(which python2) --with-pcap --with-libfixbuf --with-gnutls --enable-output-compression --enable-localtime
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
        # Fixing python 
        sed -i 's|python$|python2|' $pkgdir/usr/bin/rwidsquery $pkgdir/usr/bin/rwpcut 
}
