# Maintainer: ArchAssault <team@archassault.org>
pkgname=faraday-git
pkgver=20141003.r638
pkgrel=1
pkgdesc="A new concept (IPE) Integrated Penetration-Test Environment a multiuser Penetration test IDE. Designed for distribution, indexation and analyze of the generated data during the process of a security audit."
groups=('archassault' 'archassault-misc')
url="http://www.faradaysec.com/"
if [[ $CARCH = 'x86_64' ]]; then
       depends=('python2' 'couchdb' 'pyqt3' 'python2-pysvn' 'ipython2' 'libpqxx' 'python2-couchdbkit' 'python2-requests' 'python2-whoosh' 'python2-psycopg2'
                'python2-restkit' 'python2-mockito' 'lib32-glibc' 'ruby1.9' 'python2-ipy')
else
      depends=('python2' 'couchdb' 'pyqt3' 'python2-pysvn' 'ipython2' 'libpqxx' 'python2-couchdbkit' 'python2-requests' 'python2-whoosh' 'python2-psycopg2'
                'python2-restkit' 'python2-mockito' 'ruby1.9' 'python2-ipy')
fi
makedepends=('git')
options=('!strip')
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
source=("${pkgname}::git+https://github.com/infobyte/faraday.git")
replaces=('faraday')
conflicts=('faraday')
provides=('faraday')
sha512sums=('SKIP')

pkgver() {
    cd "${pkgname}"
    printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${pkgname}"

  #Base directories
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"
  
cat > "$pkgdir/usr/bin/faraday" << EOF
#!/bin/sh
cd /usr/share/faraday-git/
python2 ./faraday "\$@"
EOF

chmod +x "$pkgdir/usr/bin/faraday"
}

