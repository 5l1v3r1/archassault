# Maintainer: ArchAssault <team@archassault.org>
pkgname=webshag
pkgver=1.10
pkgrel=3
groups=('archassault' 'archassault-webapp' 'archassault-scanners' 'archassault-fuzzer')
pkgdesc="A multi-threaded, multi-platform web server audit tool."
url="http://www.scrt.ch/en/attack/downloads/webshag"
arch=('any')
license=('GPL3')
depends=('python2' 'wxpython2.8' 'nmap' 'nikto')
makedepends=('dos2unix')
source=("http://www.scrt.ch/outils/webshag/ws110.tar.gz"
        "webshag.install"
        "core_file.patch")
install='webshag.install'
sha512sums=('35a2af786dda2e02dae8e0edbec0e2f3ddd595fb062fb3317501e2627ceec373835ec60a769176a2b4b14df1ef2eec0a4b1acd92b5d6e59fe301c6a88832fab8'
            'e23d73499b9d8d856c0e644dac8855e7273d96390e4220097cc332744e5a4d2b38c0726b5750d7c9ae7a0a127b35df603f93810498c1bf8fc48baa361efbad76'
            '13e6248ac12a73a3003652e671e18d7d75b636af0f3ee645759e286815447e44db7ef772810255b6cc16d2ab5f49d0b2fcc8459e485858a25c62b0fe33ad1c9b')

prepare(){
  cd "$srcdir"

  find . -type f -iname "*" |
  while read i ; do
    dos2unix $i
  done

  grep -iRl python "$srcdir" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'

  cd "webshag/core"
  patch < "$srcdir/core_file.patch"
}

package() {
  cd "$srcdir"

  # Base directories.
  install -dm755 "$pkgdir/usr/share/webshag"
  install -dm755 "$pkgdir/usr/bin"

  cp -La --no-preserve=ownership * "$pkgdir/usr/share/webshag"

  # Bin.
  cat > "$pkgdir/usr/bin/webshag_cli" << EOF
#!/bin/sh
/usr/share/webshag/webshag_cli.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/webshag_cli"

cat > "$pkgdir/usr/bin/webshag_gui" << EOF
#!/bin/sh
/usr/share/webshag/webshag_gui.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/webshag_gui"
}
