# Maintainer: ArchAssault <team archassault org>
# Contributor: Meir Kriheli <mkriheli@gmail.com>

pkgname=python2-cookiecutter
_pkgname=cookiecutter
pkgver=0.9.0
pkgrel=1
pkgdesc="A command-line utility that creates projects from project templates"
arch=('any')
url="https://github.com/audreyr/cookiecutter"
license=('BSD')
depends=('python2-jinja' 'python2-yaml' 'python2-binaryornot')
makedepends=('python2-setuptools')
source=("https://github.com/audreyr/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('4aa48f3b1c84a7419a90da8b73353bf0f8ab8c16b037bae58cfb1585aad79db5d29423ecac6e404cc174f96ca2daf2bc8fc8aa5f79238fb6c1b1a3d9906e62da')

prepare(){
  grep -iRl 'python' "$srcdir/$_pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
      cd "$srcdir/${_pkgname}-${pkgver}"
      python2 setup.py install --root="$pkgdir/" --optimize=1
      install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
