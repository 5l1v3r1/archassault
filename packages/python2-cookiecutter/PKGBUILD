# Maintainer: ArchAssault <team archassault org>
# Contributor: Meir Kriheli <mkriheli@gmail.com>

pkgname=python2-cookiecutter
_pkgname=cookiecutter
pkgver=0.7.2
pkgrel=1
pkgdesc="A command-line utility that creates projects from project templates"
arch=('any')
url="https://github.com/audreyr/cookiecutter"
license=('BSD')
depends=('python2' 'python2-jinja' 'python2-yaml' 'python2-binaryornot')
makedepends=('python2-setuptools')
source=("https://github.com/audreyr/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('a6a625b0ea089527f469c02f06d1ebd42bd3d81b26f08f7e481c441b8f51eb0a27f6338b6e5f6a79d8cb238b80d879273558278b91661184bcbad12957e263f2')

prepare(){
  grep -iRl 'python' "$srcdir/$_pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
      cd "$srcdir/${_pkgname}-${pkgver}"
      python2 setup.py install --root="$pkgdir/" --optimize=1
      install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
