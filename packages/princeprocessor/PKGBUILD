# Maintainer: ArchAssault <team archassault org>
# Contributor: Cthulu201 <cthulu201@archassault.org>
 
pkgname=princeprocessor
pkgver=0.19
pkgrel=1
pkgdesc="Standalone password candidate generator using the PRINCE algorithm"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/jsteube/princeprocessor"
groups=('archassault')
license=('MIT')
depends=('gmp')
source=("https://github.com/jsteube/princeprocessor/archive/v${pkgver}.tar.gz" "makefile.patch")
sha512sums=('7c70b895085ec07c16ecd570a7eacf5c0adeea5d3df3bc70b2335cafe8ed57d70b67235f863665c028087d1b67f69f47958bc87be27bb4602a9657c29df1d6a7'
            'f5d0ccdcaea34cb33884fcfc06b7345099fc27058b4c2d829f49b3f94dab85d8a9398c48d603de788e1fa20ec4c2233a9910d2c61e4b60b2dac6038851fff909')
 
prepare(){
   cd "$srcdir/$pkgname-$pkgver/src"
   patch -Np1 -i $srcdir/makefile.patch
}

build(){
   cd "$srcdir/$pkgname-$pkgver/src"
   if [[ $CARCH == 'x86_64' ]]; then
         make
   elif [[ $CARCH == "i686" ]]; then
         make pp32
   else
         make pparm
   fi
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -dm755 "$pkgdir/usr/share/$pkgname"
    install -dm755 "$pkgdir/usr/share/$pkgname/rules"

    # rules
    install -Dm644 rules/* "$pkgdir/usr/share/$pkgname/rules/"

    # License
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 README.md "$pkgdir/usr/share/$pkgname/"
    install -Dm644 CHANGES "$pkgdir/usr/share/$pkgname/"
   
    # Bin
    if [[ $CARCH == "x86_64" ]]; then
          install -Dm755 src/pp64.bin "$pkgdir/usr/bin/$pkgname"
    elif [[ $CARCH == "i686" ]]; then
          install -Dm755 src/pp32.bin "$pkgdir/usr/bin/$pkgname"
    else
          install -Dm755 src/pparm.bin "$pkgdir/usr/bin/$pkgname"
    fi
}
