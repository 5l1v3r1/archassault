# Maintainer: ArchAssault <team at archassault org>
# Contributor: <kang@insecure.ws>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>

pkgname=audit
pkgver=2.3.7
pkgrel=1
pkgdesc='User space utilities for storing and searching the audit records generated by the audit subsystem in the Linux kernel'
url="http://people.redhat.com/sgrubb/${pkgname}"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('krb5' 'libcap-ng')
makedepends=('libldap' 'swig' 'linux-headers' 'python2')
license=('GPL')
options=('emptydirs')
backup=(
    etc/lib${pkgname}.conf
    etc/${pkgname}/${pkgname}.rules
    etc/${pkgname}/${pkgname}d.conf
    etc/audisp/audispd.conf
    etc/audisp/audisp-remote.conf
    etc/audisp/zos-remote.conf
    etc/audisp/plugins.d/af_unix.conf
    etc/audisp/plugins.d/audispd-zos-remote.conf
    etc/audisp/plugins.d/au-remote.conf
    etc/audisp/plugins.d/syslog.conf
)
source=("${url}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('301368e8d6b26a525ef22c373a969be40ee91f73941afb8370bca10ed0dd5c8f442777067744203385119ea427a184cebead22358118d42d7b85990316e4d1e2')

build() {
    cd ${pkgname}-${pkgver}
    export PYTHON=/usr/bin/python2
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --with-python=yes \
        --enable-gssapi-krb5=yes \
        --enable-systemd=yes \
        --with-libcap-ng=yes
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install

    install -d "${pkgdir}/var/log/${pkgname}"
    rm -rf "${pkgdir}/etc/rc.d" "${pkgdir}/etc/sysconfig" "${pkgdir}/usr/lib/${pkgname}"

    sed -ri 's|/sbin|/usr/bin|' \
        "${pkgdir}"/etc/${pkgname}/*.conf \
        "${pkgdir}"/etc/audisp/plugins.d/*.conf \
        "${pkgdir}"/usr/lib/systemd/system/${pkgname}d.service

    chmod 644 "${pkgdir}/usr/lib/systemd/system/${pkgname}d.service"
    mv "$pkgdir"/etc/${pkgname}/{rules.d/,}${pkgname}.rules
}
