# Maintainer: ArchAssault <team archassault org>
pkgname=mobiusft
pkgver=0.5.20
pkgrel=1
groups=('archassault' 'archassault-forensics')
pkgdesc="An open-source forensic framework written in Python/GTK that manages cases and case items, providing an abstract interface for developing extensions."
arch=('any')
url='http://savannah.nongnu.org/projects/mobiusft'
license=('GPL2')
depends=('python2' 'pygtk')
source=("http://download.savannah.gnu.org/releases/mobiusft/mobiusft-$pkgver.tar.gz")
sha512sums=('54f42076ccea96db2fefc099dc3b1aa36d08c8fa1449acfe75058d3842b9bc9bed5cb2372deb583f291800d14099e3f3cd2a8b2d380eb5699721722d6cd454ee')

prepare(){
  grep -iRl 'python' "$srcdir/mobiusft-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "$srcdir/mobiusft-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
