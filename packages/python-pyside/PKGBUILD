# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Ari Mizrahi <codemunchies@debugsecurity.com>

pkgname=python-pyside
pkgver=1.2.1
pkgrel=1
pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation. Will build for both Python 2 and 3 versions."
arch=('armv7h' 'armv6h' 'x86_64' 'i686')
license=('LGPL')
url="http://qt-project.org/wiki/PySide"
makedepends=('cmake' 'automoc4' 'qtwebkit')
depends=('python' 'qt4>=4.8' 'phonon' 'shiboken>=1.2.1' 'python-pyside-common' 'gcc-libs')
source=("http://download.qt-project.org/official_releases/pyside/pyside-qt4.8+$pkgver.tar.bz2")
md5sums=('34b05faa7cc44d3c24d5ccadd894bd3c')

build(){
    cd $srcdir/pyside-qt4.8+$pkgver
    mkdir -p build_py3 && cd build_py3

    _pyverflags=$(python -c 'import sysconfig; print(sysconfig.get_config_var("SOABI"))')

    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_TESTS=OFF \
      -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
      -DPYTHON_SUFFIX=.${_pyverflags}
    make
}

package(){
    cd $srcdir/pyside-qt4.8+$pkgver/build_py3

    make DESTDIR=$pkgdir install
    mv "$pkgdir"/usr/lib/pkgconfig/pyside.pc \
      "$pkgdir"/usr/lib/pkgconfig/pyside-py3.pc
    rm -rf "$pkgdir"/usr/include \
      "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfig.cmake \
      "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfigVersion.cmake \
      "$pkgdir"/usr/share
}
