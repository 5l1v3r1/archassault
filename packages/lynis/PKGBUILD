# Maintainer: ArchAssault <team archassault org>
pkgname=lynis
pkgver=1.4.5
pkgrel=1
groups=('archassault' 'archassault-scanners')
pkgdesc="An auditing tool for Unix (specialists)"
url='http://www.rootkit.nl/projects/lynis.html'
license=('GPL3')
arch=('any')
depends=('bash')
source=("http://cisofy.com/files/lynis-$pkgver.tar.gz")
sha512sums=('295b2b7f85e12f838febd9feb32576703c9411199dce3c398b9d897ec0f9464dbb35a8f0f7c4212a4701c5c5648985ab2bd3ea40652cee1689601b1e58500555')

package() {
  cd "$srcdir/lynis-$pkgver"

  # Profile.
  install -Dm 644 default.prf "$pkgdir/etc/lynis/default.prf"

  # Bin.
  install -Dm 755 lynis "$pkgdir/usr/bin/lynis"

  # Install plugins, include, db.
  install -d "$pkgdir/usr/share/lynis"
  cp -a --no-preserve=ownership db include plugins "$pkgdir/usr/share/lynis"
  chmod -R u=rwX,go=rX "$pkgdir/usr/share/lynis"

  # Install misc files.
  install -m644 -t "$pkgdir/usr/share/lynis" README INSTALL CHANGELOG FAQ

  # Install manpage.
  install -Dm644 lynis.8 "$pkgdir/usr/share/man/man8/lynis.8"
}
