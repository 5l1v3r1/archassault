# Maintainer: ArchAssault <team archassault org>
pkgname=lynis
pkgver=1.4.8
pkgrel=1
groups=('archassault' 'archassault-scanners')
pkgdesc="An auditing and system hardning tool for Unix/Linux"
url='http://www.rootkit.nl/projects/lynis.html'
license=('GPL3')
arch=('any')
depends=('bash')
source=("http://cisofy.com/files/lynis-$pkgver.tar.gz")
sha512sums=('ff75d5a3f6336e6497d73bbee09b84aebdeb7b6d2715edef69e2ed5f8ae516f20158d72f6e8cc920dd71c47008612f0e19ae820a345c584959cea99bcfcd2a3c')

package() {
  cd "$srcdir/lynis-$pkgver"

  # Profile.
  install -Dm 644 default.prf "$pkgdir/etc/lynis/default.prf"

  # Bin.
  install -Dm 755 lynis "$pkgdir/usr/bin/lynis"

  # Install plugins, include, db.
  install -d "$pkgdir/usr/share/lynis"
  cp -a --no-preserve=ownership db include plugins "$pkgdir/usr/share/lynis"
  chmod -R u=rwX,go=rX "$pkgdir/usr/share/lynis"

  # Install misc files.
  install -m644 -t "$pkgdir/usr/share/lynis" README INSTALL CHANGELOG FAQ

  # Install manpage.
  install -Dm644 lynis.8 "$pkgdir/usr/share/man/man8/lynis.8"
}
