# Maintainer: ArchAssault <team@archassault.org>
pkgname=hashcat-utils
pkgver=1.0
pkgrel=1
groups=('archassault' 'archassault-cracker')
pkgdesc="Utilites for Hashcat"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/wiki/doku.php?id=hashcat_utils'
license=('custom')
depends=('glibc')
makedepends=('p7zip')
source=('http://hashcat.net/tools/hashcat-utils/hashcat-utils-1.0.7z')
sha512sums=('7094b3411f350437e589d4a3ccd8a715a57a50b72d578d1675080de9f959dfc8366310e3e955170d2fdfa1977abb3f22b88b01bac28f684191cce80421005608')

prepare() {
  cd "$srcdir/hashcat-utils-$pkgver"

  sed -i 's/release: posix windows/release: posix/' Makefile
  sed -i '/WINDOWS/d' Makefile
  sed -i '/*.exe/d' Makefile
}

build() {
  cd "$srcdir/hashcat-utils-$pkgver"
  make
}

package() {
  cd "$srcdir/hashcat-utils-$pkgver"

  # Base directory.
  install -dm755 "$pkgdir/usr/bin"

  # Bin.
  for bin in $(ls *.bin)
  do
    install -m755 "$bin" "$pkgdir/usr/bin/hashcat-${bin%.bin}"
  done
}
