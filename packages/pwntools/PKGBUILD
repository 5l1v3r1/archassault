# Maintainer: ArchAssault <team archassault org>
pkgname='pwntools'
pkgver='2.1.1'
pkgrel='1'
pkgdesc='The CTF framework used by #Gallopsled in every CTF'
arch=('i668' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archassault' 'archassault-misc')
if [[ $CARCH == 'x86_64' ]]; then
      depends=('python2' 'python2-paramiko' 'python2-mako' 'lib32-glibc')
else
      depends=('python2' 'python2-paramiko' 'python2-mako')
fi
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/2.1.1.tar.gz")
sha512sums=('16934b0bec9f86890b9769300be52c2c86d719a34e2b25fe91b72ea7121d4f4f86cbcede9a3c8354a199e0661a5d75c2b9c4a02d913871c539dbf3e0d8e858b7')

prepare(){
    cd "$srcdir/$pkgname-$pkgver/bin"
    mv disasm disasm-pwntools
}

build(){
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir/" --optimize=1
    install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
