# Maintainer: ArchAssault <team@archassault.org>

pkgname=w3af
pkgver=1.6.0.5
pkgrel=3
groups=('archassault' 'archassault-webapp' 'archassault-fuzzer' 'archassault-scanners')
pkgdesc="Web Application Attack and Audit Framework"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://w3af.sourceforge.net'
license=('GPL')
depends=('python2-clamd' 'python2-pybloomfiltermmap' 'python2-esmre'
         'python2-phply' 'python2-pysqlite' 'python2-nltk' 'python2-chardet'
         'pdfminer=1:20110515-2' 'python2-futures' 'python2-pyopenssl' 'python2-lxml'
         'scapy' 'python2-guess-language' 'python2-cluster' 'python2-msgpack'
         'python2-ntlm' 'python2-pygithub' 'python2-gitpython'
         'graphviz' 'pygtksourceview2' 'pygtk' 'gtk2' 'python2-beautifulsoup3'
         'python2-xdot' 'libxml2' 'libxslt' 'halberd' 'python2-dartspylru-git')
depends_x86_64+=('lib32-glibc')
makedepends=('git')
options=('!strip')
source=("git+https://github.com/andresriancho/w3af#tag=$pkgver"
        "$pkgname.desktop" "$pkgname-no-update.patch" "$pkgname-no-pip-dependency.patch" 'archassault-version.patch')
sha512sums=('SKIP'
            '48a878492fc1958af75686bcbec33868a9e977140b52234c6c1c70a9e105a9aa08a38fa5d23d4ad7407eb193efab016b74665d02a638f1adeb401c1350d78100'
            '1cf6d9322bb21a3ca04641eb80ed38a5acae640bda6b28975667351e7debfa939ff51563e66e0508a33c5f9ac41f0bebb9944fef81886ed4f7f2cbf9d5d2d710'
            'aa270338742768707fb2b6a83ba28d91ce8e9dff553fd228d099cf721ce08f1a955ab56e11fae72a7732502bc88f309e3b9ba0b2cd5663481093289305dc9979'
            'eaf0a50f7ab801b734c3aae36e4bbf1ae1af6389d7f4a31684c0c6395bcaca677e163841cd98de0be0eafdb0c95367e6a4ae1a41d1963cad7188f2f9c4e54234')

prepare() {
  # Delete some of the stuff from w3af-extlib (stuff that needs to be installed) -> dependencies
  cd $pkgname
  rm -rf $pkgname/extlib/fpconst-0.7.2
  rm -rf $pkgname/extlib/SOAPpy

  # Patchto remove the update and set the version
  patch -Np1 -i ../$pkgname-no-update.patch
  patch -Np1 -i ../$pkgname-no-pip-dependency-check.patch
  patch -Np1 -i ../archassault-version.patch

  # Remove dependency checks.
  sed -i 's/dependency_check()/#&/' ${pkgname}_{console,gui}

  # Change the default metasploit location.
  sed -i 's|/opt/metasploit3/bin/|/usr/share/metasploit/|' $pkgname/core/controllers/misc_settings.py

  # Fix python2 hash-bang
  sed -i 's|#!\s*/usr/bin.*python.*$|#!/usr/bin/env python2|g' $(egrep -rl '\s*#!\s*/usr/bin.*python')
}

package() {
  # Install the XDG desktop file
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Install w3af
  cd $pkgname
  install -dm755 "$pkgdir/usr/share/$pkgname"
  cp --no-preserve=ownership -a * "$pkgdir/usr/share/$pkgname"

  # Install launch scripts for w3af_console and w3af_gui
  install -dm755 "$pkgdir/usr/bin"
  echo -e '#!/usr/bin/env bash\n\npython2 /usr/share/w3af/w3af_console "$@"' > "$pkgdir/usr/bin/$pkgname"
  echo -e '#!/usr/bin/env bash\n\npython2 /usr/share/w3af/w3af_gui "$@"' > "$pkgdir/usr/bin/$pkgname-gui"
  chmod 755 "$pkgdir"/usr/bin/{$pkgname,$pkgname-gui}
}
