# Maintainer: Kevin MacMartin <prurigro@gmail.com>

pkgname=yrd
pkgver=0.1
pkgrel=1
pkgdesc='A cjdns config tool for humans and cyborgs'
url='https://github.com/kpcyrd/yrd'
license=('GPL3')
arch=('any')
depends=('python2-argh' 'python2-requests' 'systemd')
install=$pkgname.install
source=("$url/archive/v$pkgver.tar.gz"
        "$pkgname.tmpfiles.conf")
sha512sums=('3582ddf7c2513f62a3747dee621a23d3418a924119bf40178cecce2e424f5f2ff7fa1021d6dd14820d7932928084cdb5d97680a62a2f3a0ee5a50e5e64f06bab'
            '685c3d6964bb5ca8df2cd1e6e3ad4776b939bd041d94776ac6ed57e8d153671cab5d225e71127eb0ed56299a81ce7466512618c80edf337194c84be563471067')

prepare() {
  cd $pkgname-$pkgver
  # Fix the python2 hash-bang
  sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' $(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
  # Configure cjdroute.conf to be located @ /etc/cjdroute.conf
  sed -i 's|/var/lib/yrd/cjdroute.conf|/etc/cjdroute.conf|' $pkgname
  # Remove self-install/update portions of the script
  sed -i '/^def install():$/,/\[+\] installation complete/d' $pkgname
  sed -i 's|install, ||' $pkgname
  sed -i -re '/(YRD|CJDNS)_(GIT_REPO|CLONE_DIR)/d' $pkgname
  # Cleanup multiple consecutive blank lines (caused by the above statements)
  sed -i '/^$/N;/^\n$/D' $pkgname
}

package() {
  # Install the tmpfiles.d config
  install -Dm644 $pkgname.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  cd $pkgname-$pkgver
  # Install the main executable
  install -Dm755 $pkgname "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/$pkgname"
  # Install a symlink to the main executable in /usr/bin
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/lib/python2.7/site-packages/$pkgname/$pkgname "$pkgdir/usr/bin/$pkgname"
  # Install the rest of the python files
  install -m644 *.py "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/"
  # Install the README.md to the shared docs folder
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
