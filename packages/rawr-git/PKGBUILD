# Maintainer: ArchAssault <team@archassault.org>
pkgname=rawr-git
pkgver=39.aac6a99
pkgrel=1
groups=('archassault' 'archassault-webapp' 'archassault-scanners')
pkgdesc="Rapid Assessment of Web Resources. A web enumerator."
arch=('any')
url="https://bitbucket.org/al14s/rawr/wiki/Home"
license=('GPL3')
makedepends=('git')
depends=('nmap' 'graphviz' 'python2-pygraphviz' 'python2-lxml' 'phantomjs' 'python2-pillow')
replaces=('rawr')
conflicts=('rawr')
provides=('rawr')
source=("git+https://bitbucket.org/al14s/rawr.git#branch=dev")
sha512sums=('SKIP')

pkgver() {
  cd rawr
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/rawr"

  sed -i '1s/env python/env python2/' rawr.py lib/requests/certs.py lib/requests/packages/chardet/chardetect.py
  sed -i '1s|/usr/bin/python|/usr/bin/python2|' lib/rawr_meta/rawr_meta.py
  sed -i '1s|/usr/local/bin/python|/usr/bin/python2|' lib/rawr_meta/lib/OleFileIO_PL.py
}

package() {
  cd "$srcdir/rawr"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin" 
  install -dm755 "$pkgdir/usr/share/rawr" 

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/rawr"

  cat > "$pkgdir/usr/bin/rawr" <<EOF
#!/bin/sh
cd /usr/share/rawr
python2 ./rawr.py
EOF

  chmod +x "$pkgdir/usr/bin/rawr"
}
