# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Th√©ophile Studer
pkgname=yara
pkgver=3.1.0
pkgrel=2
groups=('archassault' 'archassault-malware')
pkgdesc="A malware identification and classification tool"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://plusvic.github.io/${pkgname}"
depends=('jansson' 'file')
license=('APACHE')
source=("https://github.com/plusvic/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('ebebe4971d07946b5a435f0aef0784fad02e0a844b6e0add3c613528c033042e3304cef2b972d9c02307f6a5d5124cf85cb355fd666d6c0f24ab60f8d037273d')

build() {
    cd ${pkgname}-${pkgver}
    ./bootstrap.sh
    ./configure --prefix=/usr --enable-magic --enable-cuckoo
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
}
