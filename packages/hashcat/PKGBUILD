# Maintainer: ArchAssault <team@archassault.org>
pkgname=hashcat
pkgver=0.48
pkgrel=1
groups=('archassault' 'archassault-cracker')
pkgdesc="A multithreaded cross platform hash cracker."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/hashcat/'
license=('custom')
makedepends=('p7zip')
depends=('glibc')
source=("https://hashcat.net/files/hashcat-${pkgver}.7z")
sha512sums=('c49ddd130015e6c1e14d90965e26d56d7a2da51d91bb43b056faca830fdb666759d62eca35eb99c5af35397c971620814fce1182a96c3640ac1e24e167b30bbb')

prepare() {
  cd "${srcdir}/hashcat-${pkgver}"

  if [[ "$CARCH" == 'x86_64' ]]; then
    mv hashcat-cli64.bin hashcat
  else
    mv hashcat-cli32.bin hashcat
  fi
}

package() {
  cd "${srcdir}/hashcat-${pkgver}"

  rm *.{exe,bin,app}

  install -dm755 "${pkgdir}/usr/share/doc/hashcat"
  install -dm755 "${pkgdir}/etc/hashcat"

  install -Dm755 hashcat "${pkgdir}/usr/bin/hashcat"

  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/{rules,salts,tables} "${pkgdir}"/etc/"hashcat"/
  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/examples "${pkgdir}"/usr/share/doc/"hashcat"/

  # Fix permissions.
  cd "${pkgdir}"
  find . -type d -exec chmod 755 {} \+
  find . -type f -exec chmod 644 {} \+
  chmod a+x "${pkgdir}/usr/bin/hashcat"
}
